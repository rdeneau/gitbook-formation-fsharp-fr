---
description: Les fondamentaux
---

# Syntaxe

## Syntaxe - ClÃ©

1er point fort de Fâ™¯ : langage **succinct**

Pour s'en rendre compte :

1. Passons rapidement en revue sa syntaxe
2. Ensuite vous pourrez commencer Ã  jouer avec
   * â˜ C'est Ã  l'usage que l'on mesure le cÃ´tÃ© succinct de Fâ™¯

> ğŸ’¡ Fâ™¯ est gÃ©nÃ©ralement plus succinct que Câ™¯ sauf ce qui concerne le fait d'Ãªtre **explicite**.

## Commentaires

```fsharp
(* This is block
   comment *)


// And this is line comment


/// XML doc summary


/// <summary>
/// Full XML doc
/// </summary>
```

## ~~Variables~~ â†’ Valeurs

* Mot clÃ© `let` pour dÃ©clarer/nommer une valeur
* Pas besoin de `;` en fin de dÃ©claration
* Liaison/_Binding_ est immutable par dÃ©faut
  * â‰ƒ `const` en JS, `readonly` pour un membre en Câ™¯
* Mutable avec `let mutable` et opÃ©rateur d'assignation `<-`
  * â‰ƒ `let` en JS, `var` en Câ™¯
  * Avec parcimonie, sur _scope_ limitÃ©

```fsharp
let x = 1
x <- 2 // âŒ Error FS0027: Cette valeur n'est pas mutable.

let mutable x = 1
x <- 2 // âœ… AutorisÃ©
```

### PiÃ¨ges âš ï¸

Ne pas confondre la mutabilitÃ© d'une variable (la partie Ã  gauche dans le binding) et la mutabilitÃ© d'un objet (la partie Ã  droite du binding).

* `let` empÃªche le binding d'Ãªtre modifiÃ© c'est-Ã -dire affecter une nouvelle valeur Ã  une variable.
* `let` n'empÃªche pas un objet de muter...
* ... Ã  une exception prÃ¨s : les **structures mutables** (exemple : [ArrayCollector](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-core-compilerservices-arraycollector-1.html)) qui fonctionnent par valeur : quand on appelle une mÃ©thode qui modifie une structure (ex: `Add` ou `Close`), on obtient une nouvelle copie. C'est `let mutable` qui permet de garder une rÃ©fÃ©rence Ã  cette nouvelle valeur.

```fsharp
open FSharp.Core.CompilerServices

let collectorKo = ArrayCollector<int>()
collectorKo.Add(1)
collectorKo.Add(2)
let result = collectorKo.Close()
// val result: int array = [||]

let mutable collector = ArrayCollector<int>()
collector.Add(1)
collector.Add(2)
let result = collector.Close()
// result it: int array = [|1; 2|]
```

## Noms

* MÃªmes contraintes qu'en Câ™¯
* Sauf apostrophe `'`  _(tick)_
  * permise dans nom au milieu ou Ã  la fin _(mais pas au dÃ©but)_
  * en fin de nom â†’ indique une variante _(convention)_
* Entre doubles _backticks_&#x20;
  * permettent n'importe quel caractÃ¨re, en particulier les espaces, Ã  l'exception des sauts de ligne)

```fsharp
let x = 1
let x' = x + 1  // Se prononce "x prime" ou "x tick"

// Marche aussi avec les mots clÃ©s !
// Mais mieux vaut Ã©viter car c'est source de confusion !
let if' b t f = if b then t else f

let ``123 456`` = "123 456"
// ğŸ’¡ Auto-complÃ©tion : pas besoin de taper les ``, directement 123 (quand Ã§a veut marcher)
```

## _Shadowing_

* Consiste Ã  redÃ©finir une valeur avec un nom existant\
  â†’ La valeur prÃ©cÃ©dente n'est plus accessible dans le scope courant
* Interdit au niveau d'un module mais autorisÃ© dans un sous-scope
* Pratique mais peut Ãªtre trompeur\
  â†’ Peu recommandÃ©, sauf cas particulier

```fsharp
let a = 2

let a = "ko"  // ğŸ’¥ Error FS0037: DÃ©finition dupliquÃ©e de value 'a'

let b =
    let a = "ok" // ğŸ‘Œ Pas d'erreur de compilation
    // `a` ne vaut plus 2 mais "ok" dans tout le reste de l'expression `b`
    let a = "ko" // ğŸ‘Œ Plusieurs shadowings successifs sont possible !
    ...
```

## Annotation de type

* Optionnelle grÃ¢ce Ã  l'infÃ©rence
* Type dÃ©clarÃ© aprÃ¨s nom `name: type` _(comme en TypeScript)_
* Valeur obligatoire, mÃªme si `mutable`  ğŸ‘

```fsharp
let x = 1       // InfÃ©rÃ© (int)
let y: int = 2  // Explicite

let z1: int           // ğŸ’¥ Error FS0010: Construction structurÃ©e incomplÃ¨te Ã  cet emplacement...
let mutable z2: int   // ğŸ’¥ ... ou avant dans la liaison. '=' ou autre jeton attendu.
```

## Constante

* _What:_ Variable effacÃ©e Ã  la compilation, remplacÃ©e par sa valeur
  * â‰ƒ `const` Câ™¯ - mÃªme idÃ©e en TS que `const enum`
* _How:_ Valeur dÃ©corÃ©e avec attribut `Literal` \
  âš ï¸ Attributs entre `[< >]` \
  â†’ Erreur frÃ©quente au dÃ©but : utiliser `[ ]` _(comme en Câ™¯)_
* Convention de nommage recommandÃ©e : PascalCase

```fsharp
[<Literal>] // Saut de ligne nÃ©cessaire car avant le `let`
let AgeOfMajority = 18

let [<Literal>] Pi = 3.14 // Possible aussi aprÃ¨s le `let`
```

## Nombre

```fsharp
let pi = 3.14             // val pi : float = 3.14       â€¢ System.Double
let age = 18              // val age : int = 18          â€¢ System.Int32
let price = 5.95m         // val price : decimal = 5.95M â€¢ System.Decimal
```

âš ï¸ Pas de conversion implicite entre nombre

â†’ ğŸ’¡ Utiliser fonctions `int`, `float`, `decimal`

<pre class="language-fsharp"><code class="lang-fsharp"><strong>let i = 1
</strong><strong>i * 1.2;;  // ğŸ’£ error FS0001: The type 'float' does not match the type 'int'
</strong><strong>
</strong><strong>float 3;;             // val it : float = 3.0
</strong>decimal 3;;           // val it : decimal = 3M
int 3.6;;             // val it : int = 3
int "2";;             // val it : int = 2
</code></pre>

â˜ï¸ Cette rÃ¨gle a Ã©tÃ© assouplie dans certains cas avec [F# 6](https://learn.microsoft.com/en-us/dotnet/fsharp/whats-new/fsharp-6#additional-implicit-conversions).

## String

```fsharp
let name = "Bob"              // val name : string = "Bob"

// ChaÃ®ne avec interpolation
let name2 = $"{name} Marley"  // val name' : string = "Bob Marley"
let name2 = sprintf "{0} Marley" name // Equivalent avant Fâ™¯ 5

// AccÃ¨s Ã  un caractÃ¨re par son index (>= 0)
let initial = name2[0]         // val initial :  char = 'B'
let initial = name2.[0]        // â˜ï¸ Syntaxe avant Fâ™¯ 6

// AccÃ¨s Ã  une sous-chaÃ®ne via une plage (EN: range)
// ğŸ’¡ Alternative Ã  la mÃ©thode Substring(index [, length])
let firstName = name2[0..2]   // val firstName : string = "Bob"
let firstName = name2.[0..2]  // â˜ï¸ Syntaxe avant Fâ™¯ 6
```

Syntaxes complÃ©mentaires :

```fsharp
// Verbatim string: idem Câ™¯
let verbatimXml = @"<book title=""Paradise Lost"">"

// Triple-quoted string : pas besoin d'Ã©chapper les guillemets `"`
let tripleXml = """<book title="Paradise Lost">"""

// Les chaÃ®nes normales acceptent les sauts de ligne mais ne trimment pas les whitespaces
let poemIndented = "
    The lesser world was daubed
    By a colorist of modest skill
    A master limned you in the finest inks
    And with a fresh-cut quill."

// Solution : les backslash strings :
// Les whitespaces (espace et saut de ligne) sont ignorÃ©s 
// entre le \ terminant une ligne et le caractÃ¨re non-whitespace suivant
// (d'oÃ¹ les \n pour remettre des sauts de ligne) :
let poem = "\
    The lesser world was daubed\n\
    By a colorist of modest skill\n\
    A master limned you in the finest inks\n\
    And with a fresh-cut quill."

// On peut aussi combiner sauts de ligne et backslash strings :
let poemWithoutBackslashN = "\
    The lesser world was daubed
\
    By a colorist of modest skill
\
    A master limned you in the finest inks
\
    And with a fresh-cut quill."
```

### ChaÃ®ne avec interpolation en F# 8

Une chaÃ®ne interpolÃ©e ne peut pas contenir d'accolades (`$"{xxx}"`) sauf Ã  les doubler (`$"{{xxx}}"`) . Depuis F# 8, c'est le caractÃ¨re $ que l'on double (`$$`) pour indiquer le nombre d'accolades Ã  partir duquel il y a interpolation.

```fsharp
let classAttr = "bold"
let cssNew = $$""".{{classAttr}}:hover {background-color: #eee;}"""
```

### Encodage

Les littÃ©raux de chaÃ®nes (EN: _string literals_) sont encodÃ©s en Unicode.&#x20;

```fsharp
let unicodeString1 = "abc"  // val unicodeString1: string = "abc"
let unicodeString2 = "abâœ…" // val unicodeString2: string = "abâœ…"
```

On peut travailler en ASCII grÃ¢ce au suffixe `B` mais on manipule alors un tableau de bytes :

```fsharp
let asciiBytes1 = "abc"B
// val asciiBytes1: byte array = [|97uy; 98uy; 99uy|]

let asciiBytes2 = "abâœ…"B
// ğŸ’¥ Error FS1140: This byte array literal contains characters
//    that do not encode as a single byte
```

## Listes

Liste immuable â†’ type spÃ©cial Fâ™¯ â‰  `System.Collection.Generic.List<T>`

```fsharp
let abc = [ 'a'; 'b'; 'c' ] // val abc : char list = ['a'; 'b'; 'c']
let a =
  [ 2
    3 ]  // val a : int list = [2; 3]
```

* CrÃ©ation avec `[]`, Ã©lÃ©ments sÃ©parÃ©s par `;` ou saut de ligne + indentation
  * :warning: PiÃ¨ge : ne pas utiliser `,` sinon on a en fait un seul Ã©lÃ©ment, un tuple ! ğŸ“[tuples.md](../types-composites/tuples.md "mention")
* Notation ML du type `int list` = `List<int>`
  * â˜ Idiomatique que pour `array`, `list` et `option` ğŸ“[type-option.md](../types-monadiques/type-option.md "mention")

### OpÃ©rateurs sur les listes

* `::`  _Cons_ _(pour "construction")_\
  â†’ Ajoute un element en tÃªte de liste
* `@` _Append_\
  â†’ ConcatÃ¨ne 2 listes
* `..` _Range_\
  â†’ `min..max`: plage de nombres ou de caractÃ¨res entre le min et le max spÃ©cifiÃ©s _(inclus)_\
  â†’ `min..step..max`: `step` dÃ©finit l'Ã©cart entre 2 nombres successifs dans la plage

```fsharp
let nums = [2..5]                 // val nums : int list = [2; 3; 4; 5]
let nums' = 1 :: nums             // val nums' : int list = [1; 2; 3; 4; 5]

let chars = [ 'a' .. 'd' ]        // val chars : char list = ['a'; 'b'; 'c'; 'd']
let chars' = chars @ [ 'e'; 'f' ] // val chars' : char list = ['a'; 'b'; 'c'; 'd'; 'e'; 'f']
let e = chars'[4]                 // val e: char = 'e'
```

âš ï¸ Il faut mettre un espace avant `[]` pour crÃ©er une liste, pour diffÃ©rencier de l'accÃ¨s par index.

### Module `List`

Ce module contient les fonctions pour manipuler une ou plusieurs listes.

| Fâ™¯ List            | Câ™¯ LINQ (IEnumerable)      | JS Array             |
| ------------------ | -------------------------- | -------------------- |
| `map`, `collect`   | `Select()`, `SelectMany()` | `map()`, `flatMap()` |
| `exists`, `forall` | `Any(predicate)`, `All()`  | `some()`, `every()`  |
| `filter`           | `Where()`                  | `filter()`           |
| `find`, `tryFind`  | Ã—                          | `find()`             |
| `fold`, `reduce`   | `Aggregate([seed]])`       | `reduce()`           |
| `average`, `sum`   | `Average()`, `Sum()`       | Ã—                    |

â˜ **Autres fonctions :** cf. [documentation](syntaxe.md#syntaxe-cle)

## Fonctions

* Fonction nommÃ©e : dÃ©clarÃ©e avec `let`
* Convention de nommage : **camelCase**
* Pas de `return` : la fonction renvoie toujours la derniÃ¨re expression dans son corps
* Pas de `()` autour de tous les paramÃ¨tres
  * `()` autour d'un paramÃ¨tre avec annotation de type (1) ou dÃ©construit tel qu'un tuple (2)

```fsharp
let square x = x * x  // Fonction Ã  1 paramÃ¨tre
let res = square 2    // Vaut 4

// (1) ParenthÃ¨ses nÃ©cessaires lors d'annotations de type
let square' (x: int) : int = x * x

// (2) ParenthÃ¨ses nÃ©cessaires lors de dÃ©construction d'un paramÃ¨tre
let addTuple (x, y) = x + y
```

### Fonctions de 0-n paramÃ¨tres

* ParamÃ¨tres et arguments sÃ©parÃ©s par **espace**
  * :warning:ï¸ PiÃ¨ge : `,` sert Ã  instancier 1 tuple = 1! paramÃ¨tre
* `()` : fonction sans paramÃ¨tre, sans argument
* Sans `()`, on dÃ©clare une valeur "vide", pas une fonction :

```fsharp
let add x y = x + y  // Fonction Ã  2 paramÃ¨tres
let res = add 1 2    // `res` vaut 3

let printHello () = printfn "Hello"  // Fonction sans paramÃ¨tre
printHello ()                        // Affiche "Hello" en console

let notAFunction = printfn "Hello"   // Affiche "Hello" en console et renvoie "vide"
```

### Fonction multi-ligne

**Indentation** nÃ©cessaire, mais pas de `{}`

```fsharp
let evens list =
    let isEven x =  // ğŸ‘ˆ Sous-fonction
        x % 2 = 0   // ğŸ’¡ `=` opÃ©rateur d'Ã©galitÃ© - Pas de `==`
    List.filter isEven list

let res = evens [1;2;3;4;5] // Vaut [2;4]
```

### Fonction anonyme

A.k.a. **Lambda**, arrow function

* DÃ©clarÃ©e avec `fun` et `->`
* En gÃ©nÃ©ral entre `()` pour question de prÃ©cÃ©dence

```fsharp
let evens' list = List.filter (fun x -> x % 2 = 0) list
```

â˜ **Note** : taille de la flÃ¨che

* Fine `->` en Fâ™¯, Java
* Large / _fat_ `=>` en Câ™¯, JS

## Convention de noms courts

* `x`, `y`, `z` : paramÃ¨tres de type valeurs simples
* `f`, `g`, `h` : paramÃ¨tres de type fonction
* `xs` : liste de `x` â†’ `x::xs` (ou `h::t`) = _head_ et _tail_ d'une liste non vide
* `_` : _discard_ / Ã©lÃ©ment ignorÃ© car non utilisÃ© _(comme en Câ™¯ 7.0)_

Bien adaptÃ© quand fonction courte ou trÃ¨s gÃ©nÃ©rique :

```fsharp
// Fonction qui renvoie son paramÃ¨tre d'entrÃ©e, quel qu'il soit
let id x = x

// Composition de 2 fonctions
let compose f g = fun x -> g (f x)
```

## _Piping_

OpÃ©rateur _pipe_ `|>` : mÃªme idÃ©e que `|` UNIX \
â†’ Envoyer la valeur Ã  gauche dans une fonction Ã  droite \
â†’ Ordre naturel "sujet verbe" - idem appel mÃ©thode d'un objet

```fsharp
let a = 2 |> add 3  // Se lit comme "2 + 3"

let nums = [1;2;3;4;5]
let evens = nums |> List.filter (fun x -> x % 2 = 0)
// Idem     List.filter (fun x -> x % 2 = 0) nums
```

```csharp
// â‰ƒ Câ™¯
var a = 2.Add(3);
var nums = new[] { 1, 2, 3, 4, 5 };
var evens = nums.Where(x => x % 2 == 0);
```

### Chainage de _pipes_ - _Pipeline_

Comme fluent API en Câ™¯ mais natif : pas besoin de mÃ©thode d'extension ğŸ‘

FaÃ§on naturelle de reprÃ©senter le flux de donnÃ©es entre diffÃ©rentes opÃ©rations â†’ Sans variable intermÃ©diaire ğŸ‘

Ã‰criture :

```fsharp
// Sur une seule ligne (courte)
let res = [1;2;3;4;5] |> List.filter (fun x -> x % 2 = 0) |> List.sum

// Sur plusieurs lignes => fait ressortir les diffÃ©rentes opÃ©rations
let res' =
    [1; 2; 3; 4; 5]
    |> List.filter isOdd  // Avec `let isOdd x = x % 2 <> 0`
    |> List.map square    //      `let square x = x * x`
    |> List.map addOne    //      `let addOne x = x + 1`
```

## Expression `if/then/else`

ğŸ’¡ `if b then x else y` â‰ƒ OpÃ©rateur ternaire Câ™¯ `b ? x : y`

```fsharp
let isEven n =
    if n % 2 = 0 then
        "Even"
    else
        "Odd"
```

â˜ Si `then` ne renvoie pas de valeur, `else` facultatif

```fsharp
let printIfEven n msg =
    if n |> isEven then
        printfn msg
```

## Expression `match`

```fsharp
let translate civility =
    match civility with
    | "Mister" -> "Monsieur"
    | "Madam"  -> "Madame"
    | "Miss"   -> "Mademoiselle"
    | _        -> ""   // ğŸ‘ˆ wilcard `_`
```

Ã‰quivalent en Câ™¯ 8 :

```csharp
public static string Translate(string civility) =>
    civility switch {
        "Mister" => "Monsieur"
        "Madam"  => "Madame"
        "Miss"   => "Mademoiselle"
        _        => ""
    }
```

## Exception

### Handling Exception

â†’ Bloc `try/with` -- :warning: PiÃ¨ge : ~~try/catch~~

```fsharp
let tryDivide x y =
   try
       Some (x / y)
   with :? System.DivideByZeroException ->
       None
```

ğŸ’¡  Il n'y a pas de `try/with/finally` mais on peut faire imbriquer un `try/finally` dans un `try/with`.

### Throwing Exception

â†’ Helpers `failwith`, `invalidArg` et `nullArg`

```fsharp
let fn arg =
    if arg = null then nullArg (nameof arg)
    failwith "Not implemented"

let divide x y =
    if y = 0
    then invalidArg (nameof y) "Divisor cannot be zero"
    else x / y
```

â˜ Pour erreurs mÃ©tier i.e. cas prÃ©vus, non exceptionnels : \
â†’ PrÃ©fÃ©rer le type `Result` a.k.a _Railway-oriented programming_ ğŸ“ [Broken link](broken-reference "mention")

ğŸ”— Handling Errors Elegantly [https://devonburriss.me/how-to-fsharp-pt-8/](https://devonburriss.me/how-to-fsharp-pt-8/)

## Attributs

Les attributs fonctionnent comme en C# pour dÃ©corer une classe, une mÃ©thode, etc.

La subtilitÃ© vient de la syntaxe :&#x20;

* C# `[AttributUn][DeuxiemeAttribut]` ou `[AttributUn, DeuxiemeAttribut]`\
  â†’ Attributs entre `[...]` et sÃ©parÃ©s par des `,`
* F# `[<AttributUn>][<DeuxiemeAttribut>]` ou `[<AttributUn>; <DeuxiemeAttribut>]` \
  â†’ Attributs entre `[<...>]` et sÃ©parÃ©s par des `;`

## Ordre des dÃ©clarations

:warning: DÃ©clarations ordonnÃ©es de haut en bas

* DÃ©claration prÃ©cÃ¨de usage
* Au sein d'un fichier
* Entre fichiers dÃ©pendants
  * â˜ Importance de l'ordre des fichiers dans un `.fsproj`
* BÃ©nÃ©fice : pas de dÃ©pendances cycliques ğŸ‘

```fsharp
let result = fn 2
//           ~~ ğŸ’¥ Error FS0039: La valeur ou le constructeur 'fn' n'est pas dÃ©fini

let fn i = i + 1 // â˜ Doit Ãªtre dÃ©clarÃ©e avant `result`
```

## Indentation

* TrÃ¨s importante pour lisibilitÃ© du code
  * CrÃ©e struct. visuelle qui reflÃ¨te struct. logique / hiÃ©rarchie
  * `{}` alignÃ©es verticalement (Câ™¯) = aide visuelle mais < indentation
* Essentielle en Fâ™¯ :
  * FaÃ§on de dÃ©finir des blocs de code
  * Compilateur assure que indentation est correcte

ğŸ‘‰ **Conclusion :**

* Fâ™¯ force Ã  bien indenter
* Mais c'est pour notre bien
* Car c'est bÃ©nÃ©fique pour lisibilitÃ© du code ğŸ‘

### Ligne verticale d'indentation

â†’ DÃ©marre aprÃ¨s `let ... =`, `(`, `then`/`else`, `try`/`finally`, `do`, `->` (dans clause `match`) mais pas `fun` ! â†’ Commence au 1er caractÃ¨re non _whitespace_ qui suit â†’ Tout le reste du bloc doit s'aligner verticalement â†’ L'indentation peut varier d'un bloc Ã  l'autre

```
let f =
  let x=1     // ligne d'indentation fixÃ©e en column 3 (indÃ©pendamment des lignes prÃ©cÃ©dentes)
  x+1         // ğŸ‘‰ cette ligne (du mÃªme bloc) doit commencer en column 3 ; ni 2, ni 4 â—

let f = let x=1  // Indentation en column 10
        x+1      // ğŸ‘‰ alignement vertical des autres lignes du bloc en column 10
```

ğŸ”— [https://fsharpforfunandprofit.com/posts/fsharp-syntax/](https://fsharpforfunandprofit.com/posts/fsharp-syntax/)

### Recommendations / Indentation

* Utiliser des **espaces**, pas des ~~tabulations~~
* Utiliser **4 espaces** par indentation
  * Facilite la dÃ©tection visuelle des blocs
  * ... qui ne peut se baser sur les `{ }` comme en Câ™¯
* Ã‰viter un **alignement sensible au nom**, a.k.a _vanity alignment_
  * Risque de rupture de l'alignement aprÃ¨s renommage â†’ ğŸ’¥ Compilation
  * Bloc trop dÃ©calÃ© Ã  droite â†’ nuit Ã  la lisibilitÃ©

```fsharp
// ğŸ‘Œ OK
let myLongValueName =
    someExpression
    |> anotherExpression

// âš ï¸ Ã€ Ã©viter
let myLongValueName = someExpression
                      |> anotherExpression  // ğŸ‘ˆ DÃ©pend de la longueur de `myLongValueName`
```

&#x20;â˜ï¸ Les rÃ¨gles concernant l'indentation ont Ã©tÃ© assouplies en [F# 6](https://learn.microsoft.com/en-us/dotnet/fsharp/whats-new/fsharp-6#indentation-syntax-revisions).

## ComplÃ©ment

:link: [Troubleshooting F# - Why won't my code compile?](https://fsharpforfunandprofit.com/troubleshooting-fsharp/)
