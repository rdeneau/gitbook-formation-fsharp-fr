# Types

## Type `List`

Impl√©ment√©e sous forme de **liste simplement cha√Æn√©e** : ‚Üí 1 liste = 1 √©l√©ment _(Head)_ + 1 sous-liste _(Tail)_ ‚Üí Construction nomm√©e _Cons_ et not√©e `::`

Pour √©viter r√©cursion infinie, besoin d'un cas de "sortie" : ‚Üí Liste vide nomm√©e _Empty_ et not√©e `[]`

üëâ **Type union g√©n√©rique et r√©cursif** :

```fsharp
type List<'T> =
  | ( [] )
  | ( :: ) of head: 'T * tail: List<'T>
```

### Alias de types

* Par d√©faut, `List` fait r√©f√©rence au type (ou au module) F#
* `list` est un alias du type `List` de F#, utilis√© souvent avec la notation OCaml : \
  ‚Üí `let l : string list = ...`
* Si on fait un `open System.Collections.Generic`, `List` fera r√©f√©rence √† la liste mutable C# et masquera le type `List` F# mais sera fusionn√© avec le module `List` F# !
* Pour √©viter cela, on peut utiliser l'alias `ResizeArray` pour r√©f√©rencer la liste mutable C# directement, sans devoir faire `open System.Collections.Generic`.

### Litt√©raux

| Nb | Notation    | Notation explicite  | Signification                       |
| -- | ----------- | ------------------- | ----------------------------------- |
| 0  | `[]`        | `[]`                | Empty                               |
| 1  | `[1]`       | `1 :: []`           | Cons (1, Empty)                     |
| 2  | `[2; 1]`    | `2 :: 1 :: []`      | Cons (2, Cons (1, Empty))           |
| 3  | `[3; 2; 1]` | `3 :: 2 :: 1 :: []` | Cons (3, Cons (2, Cons (1, Empty))) |

V√©rification par d√©compilation avec [SharpLab.io](https://sharplab.io/#v2:DYLgZgzgPsCmAuACAbgBkQXkQbQLoFgAoOJZARkxzIOIRQCZLt6BuRaoklAZie7dbsaRIA==) :

```cs
//...
v1@2 = FSharpList<int>.Cons(1, FSharpList<int>.Empty);
v2@3 = FSharpList<int>.Cons(2, FSharpList<int>.Cons(1, FSharpList<int>.Empty));
//...
```

### Immuable

Il n'est pas possible de modifier une liste existante. \
‚Üí C'est cela qui permet de l'impl√©menter en liste cha√Æn√©e.

üí° L'id√©e est de cr√©er une nouvelle liste pour signifier un changement.\
&#x20;‚Üí Utiliser les op√©rateurs _Cons_ (`::`) et _Append_ (`@`) üìç

### Initialisation

```fsharp
// Range: Start..End (Step=1)
let numFromOneToFive = [1..5]     // [1; 2; 3; 4; 5]

// Range: Start..Step..End
let oddFromOneToNine = [1..2..9]  // [1; 3; 5; 7; 9]

// Comprehension
let pairs =
    [ for i in [1..3] do
      for j in [1..3] do
        (i, j) ]              // [(1, 1); (1; 2); (1; 3); (2, 1); ... (3, 3)]
```

### Exercices üïπÔ∏è

#### **1.** Impl√©menter la fonction `rev`

Inverse une liste : `rev [1; 2; 3]` ‚â° `[3; 2; 1]`

#### **2.** Impl√©menter la fonction `map`

Transforme chaque √©l√©ment : `[1; 2; 3] |> map ((+) 1)` ‚â° `[2; 3; 4]`

üí° **Astuces**\
‚Üí Pattern matching liste vide `[]` ou _Cons_ `head :: tail` \
‚Üí Sous-fonction _(tail-) recursive_

#### Solution üé≤

```fsharp
let rev list =
    let rec loop acc rest =
        match rest with
        | [] -> acc
        | x :: xs -> loop (x :: acc) xs
    loop [] list

let map f list =
    let rec loop acc rest =
        match rest with
        | [] -> acc
        | x :: xs -> loop (f x :: acc) xs
    list |> loop [] |> rev
```

üí° V√©rification avec [sharplab.io](https://sharplab.io/#v2:DYLgZgzgNAJiDUAfYBTALgAgE4oG4eAEsJMBeAWACgMaD1sUBjAgexYAcMBDR5nEjBWq0RAWy5pGACwYCA7oTRSqIkYgwBtALoYAtAD5uvFaprqAHhhAgM5iHsPA2nABSXrRxgEpbEEzScOTR0iEiogA) de la _tail recursion_ compil√©e en boucle `while`

#### V√©rification en console FSI ‚úÖ

```fsharp
// Tests en console FSI
let (=!) actual expected =
    if actual = expected
    then printfn $"‚úÖ {actual}"
    else printfn $"‚ùå {actual} != {expected}"

[1..3] |> rev =! [3; 2; 1];;
// ‚úÖ [3; 2; 1]

[1..3] |> map ((+) 1) =! [2; 3; 4];;
// ‚úÖ [2; 3; 4]
```

## Type `Array`

* Diff√©rences / `List` : mutable, taille fixe, acc√®s index√© en O(1)
* Signature g√©n√©rique : `'T array` _(r√©cemment recommand√©e)_ ou `'T []`
* Litt√©ral et _comprehension_ : similaires √† `List`

```fsharp
// Litt√©ral
[| 1; 2; 3; 4; 5 |]  // val it : int [] = [|1; 2; 3; 4; 5|]

// Comprehension using range
[| 1 .. 5 |] = [| 1; 2; 3; 4; 5 |]  // true
[| 1 .. 3 .. 10 |] = [| 1; 4; 7; 10 |] // true

// Comprehension using generator
[| for a in 1 .. 5 do (a, a * 2) |]
// [|(1, 2); (2, 4); (3, 6); (4, 8); (5, 10)|]
```

### Acc√®s index√© & mutation

Acc√®s par index : `my-array.[my-index]`

:warning: **Pi√®ge :** ne pas oublier le `.` avant les crochets `[]` ‚ùó\
üéÅ **F‚ôØ 6.0** supporte sans le `.` : `my-array[my-index]`

```fsharp
let names = [| "Juliet"; "Monique"; "Rachelle"; "Tara"; "Sophia" |]
names.[4] <- "Kristen" // "Rachelle"
names    // [| "Juliet"; "Monique"; "Rachelle"; "Tara"; "Kristen" |]
         //                                              ^^^^^^^
```

### _Slicing_

Renvoie un sous-tableau entre les indices `start..end` optionnels

```fsharp
let names = [|"0: Juliet"; "1: Monique"; "2: Rachelle"; "3: Tara"; "4: Sophia"|]

names.[1..3]  // [|"1: Monique"; "2: Rachelle"; "3: Tara"|]
names.[2..]   // [|"2: Rachelle"; "3: Tara"; "4: Sophia"|]
names.[..3]   // [|"0: Juliet"; "1: Monique"; "2: Rachelle"; "3: Tara"|]
```

üí° Marche aussi avec une `string` : `"012345".[1..3]` ‚â° `"123"`

## Alias `ResizeArray`

‚òùÔ∏è Ne pas confondre le type `List<T>` de F‚ôØ avec celui de  .NET dans le namespace [System.Collections.Generic](https://learn.microsoft.com/en-us/dotnet/api/system.collections.generic?view=net-8.0).

M√™me si les listes .NET ne sont pas tr√®s souvent utilis√©es en F‚ôØ, quand on en a besoin (par exemple en interop), il vaut mieux utiliser l'alias `ResizeArray` qui permet d'√©viter la confusion avec les listes F‚ôØ.

```fsharp
let rev items = items |> Seq.rev |> ResizeArray
let initial = ResizeArray [ 1..5 ]
let reversed = rev initial // ResizeArray [ 5..-1..0 ]
```

‚òùÔ∏è Ne pas confondre l'alias `ResizeArray` avec le type `Array` de F‚ôØ.

## Type `Seq`

`type Seq<'T> = IEnumerable<'T>` ‚Üí S√©rie d'√©l√©ments de m√™me type

_Lazy_ : s√©quence construite au fur et √† mesure lors de son it√©ration ‚â† `List` construite d√®s la d√©claration

‚Üí Peut offrir de meilleures performances qu'un `List` pour une collection avec beaucoup d'√©l√©ments et qu'on ne souhaite pas parcourir enti√®rement.

### Syntaxe

`seq { comprehension }`

```fsharp
seq { yield 1; yield 2 }   // 'yield' explicites üòï
seq { 1; 2; 3; 5; 8; 13 }  // 'yield' implicites üëç

// Range
seq { 1 .. 10 }       // seq [1; 2; 3; 4; ...]
seq { 1 .. 2 .. 10 }  // seq [1; 3; 5; 7; ...]

// G√©n√©rateur
seq { for a in 1 .. 5 do (a, a * 2) }
// seq [(1, 2); (2, 4); (3, 6); (4, 8); ...]
```

### S√©quence infinie

**Option 1** : appeler la fonction `Seq.initInfinite` : ‚Üí `Seq.initInfinite : (initializer: (index: int) -> 'T) -> seq<'T>` ‚Üí Param√®tre `initializer` sert √† cr√©er l'√©l√©ment d'index (>= 0) sp√©cifi√©

**Option 2** : √©crire une fonction r√©cursive g√©n√©rant la s√©quence

```fsharp
// Option 1
let seqOfSquares = Seq.initInfinite (fun i -> i * i)

// Option 2
let seqOfSquares' =
    let rec loop n = seq { yield n * n; yield! loop (n+1) }
    loop 0

// Test
let firstTenSquares = seqOfSquares |> Seq.take 5 |> List.ofSeq // [0; 1; 4; 9; 16]
```

## Type `Set`

Collection auto-ordonn√©e d'√©l√©ments uniques _(sans doublon)_ ‚Üí Impl√©ment√©e sous forme d'arbre binaire

```fsharp
// Cr√©ation
set [ 2; 9; 4; 2 ]          // set [2; 4; 9]  // ‚òù √âl√©ment 2 d√©doublonn√©
Set.ofArray [| 1; 3 |]      // set [1; 3]
Set.ofList [ 1; 3 ]         // set [1; 3]
seq { 1; 3 } |> Set.ofSeq   // set [1; 3]

// Ajout/retrait d'√©l√©ment
Set.empty         // set []
|> Set.add 2      // set [2]
|> Set.remove 9   // set [2]    // ‚òù Pas d'exception
|> Set.add 9      // set [2; 9]
|> Set.remove 9   // set [2]
```

### Informations

‚Üí `count`, `minElement`, `maxElement`

```fsharp
let oneToFive = set [1..5]          // set [1; 2; 3; 4; 5]

// Nombre d'√©l√©ments : propri√©t√© `Count` ou fonction `Set.count` - ‚ö†Ô∏è O(N)
// ‚òù Ne pas confondre avec `Xxx.length` pour Array, List, Seq
let nb = Set.count oneToFive  // 5

// √âl√©ment min, max
let min = oneToFive |> Set.minElement   // 1
let max = oneToFive |> Set.maxElement   // 5
```

### Op√©rations

‚Üí **Union**, **Diff√©rence**, **Intersection** _(idem ensembles en Math)_

| Op√©ration    | ? | Op√©rateur | Fonction 2 sets  | Fonction N sets      |
| ------------ | - | --------- | ---------------- | -------------------- |
| Union        | ‚à™ | `+`       | `Set.union`      | `Set.unionMany`      |
| Diff√©rence   | ‚äñ | `-`       | `Set.difference` | `Set.differenceMany` |
| Intersection | ‚à© |           | `Set.intersect`  | `Set.intersectMany`  |

**Exemples :**

```fsharp
let oneToFive = set [1..5]                 // A - set [1; 2; 3; 4; 5]
let evenToSix = set [2; 4; 6]              // B - set [2; 4; 6]

let union = oneToFive + evenToSix              // set [1; 2; 3; 4; 5; 6]
let diff  = oneToFive - evenToSix              // set [1; 3; 5]
let inter = Set.intersect oneToFive evenToSix  // set [2; 4]
```

| Valeur   | Union                                        | Diff√©rence                                   | Intersection                                 |
| -------- | -------------------------------------------- | -------------------------------------------- | -------------------------------------------- |
| A        | `[ 1 2 3 4 5   ]`                            | `[ 1 2 3 4 5   ]`                            | `[ 1 2 3 4 5   ]`                            |
| B        | `[   2   4   6 ]`                            | `[   2   4   6 ]`                            | `[   2   4   6 ]`                            |
| R√©sultat | <p>A + B<br><code>[ 1 2 3 4 5 6 ]</code></p> | <p>A - B<br><code>[ 1   3   5   ]</code></p> | <p>A ‚à© B<br><code>[   2   4     ]</code></p> |

## Type `Map`

Tableau associatif { _Cl√©_ ‚Üí _Valeur_ } ‚âÉ `Dictionary` immutable en C‚ôØ

```fsharp
// Cr√©ation : depuis collection de tuples (key, val)
// ‚Üí Fonction `Map.ofXxx` (Array, List, Seq)
let map1 = seq { (2, "A"); (1, "B") } |> Map.ofSeq
// ‚Üí Constructeur `Map(tuples)`
let map2 = Map [ (2, "A"); (1, "B"); (3, "C"); (3, "D") ]
// map [(1, "B"); (2, "A"); (3, "D")]
// üëâ Ordonn√©s par cl√©s (1, 2, 3) et d√©doublonn√©s en last win - cf. { 3 ‚Üí "D" }

// Ajout/retrait d'√©l√©ment
Map.empty         // map []
|> Map.add 2 "A"  // map [(2, "A")]
|> Map.remove 5   // map [(2, "A")] // ‚òù Pas d'exception si cl√© absente
|> Map.add 9 "B"  // map [(2, "A"); (9, "B")]
|> Map.remove 2   // map [(9, "B")]
```

### Acc√®s par cl√©

```fsharp
let table = Map [ (2, "A"); (1, "B"); (3, "D") ]

// Syntaxe `.[key]` ou juste `[key]` depuis F‚ôØ 6
table.[1]  // "B"  // ‚ö†Ô∏è `1` est bien une cl√© et pas un indice
table.[0]  // üí• KeyNotFoundException

// Fonction `Map.find` : renvoie valeur ou üí• si cl√© absente
table |> Map.find 3     // "D"

// Fonction `Map.tryFind` : renvoie `'V option`
table |> Map.tryFind 3  // Some "D"
table |> Map.tryFind 9  // None
```

## Dictionnaires

### `dict` function

Construit un `IDictionary<'k, 'v>` immutable ‚ùï √† partir d'une s√©quence de pairs cl√©/valeur

```fsharp
let table = dict [ (1, 100); (2, 200) ] // System.Collections.Generic.IDictionary<int,int>

table[1]  // ‚Üí 100

table[99] // üí£ KeyNotFoundException

table[1] <- 111 // üí£ NotSupportedException: This value cannot be mutated
table.Add(3, 300) // üí£ NotSupportedException: This value cannot be mutated
```

### `readOnlyDict` function

Construit un `IReadOnlyDictionary<'k, 'v>` √† partir d'une s√©quence de pairs cl√©/valeur

```fsharp
let table = readOnlyDict [ (1, 100); (2, 200) ]
table[1]  // 100
table[99] // üí£ KeyNotFoundException

// Ne compile pas :
table[1] <- 111   // ‚ö†Ô∏è Property 'Item' cannot be set (FS810)
table.Add(3, 300) // ‚ö†Ô∏è The type 'IReadOnlyDictionary<_,_>' does not define the field, constructor or member 'Add' (FS39)
```

> üëâ **Recommendation**\
> Pr√©f√©rer `readOnlyDict` √† `dict` car l'interface `IReadOnlyDictionary<'k, 'v>` est + fid√®le √† l'impl√©mentation sous-jacente. Au contraire, `dict` ne respecte pas le principe de substitution de Liskov !

### `KeyValue` active pattern

* Permet de d√©construire une entr√©e `KeyValuePair` d'un dictionnaire \
  en passant par un tuple `(cl√©, valeur)`
*   Signature: &#x20;

    ```fsharp
    ( |KeyValue| ) : KeyValuePair<'Key,'Value> -> 'Key * 'Value
    ```

```fsharp
let table =
    readOnlyDict
        [ (1, 100)
          (2, 200)
          (3, 300) ]

// Parcourt du dictionnaire
for kv in table do // kv: KeyValuePair<int,int>
    printfn $"{kv.Key}, {kv.Value}"

// De mani√®re + √©l√©gante avec l'active pattern
for KeyValue (key, value) in table do
    printfn $"{key}, {value}"
```

## Performance des lookups (`find`)

üîó [High Performance Collections in F#](https://www.compositional-it.com/news-blog/high-performance-hash-tables-for-f/), Compositional IT, Jan 2021

### `Map` vs `Dictionary`

Fonction `readOnlyDict` permet de cr√©er rapidement un `IReadOnlyDictionary` ‚Üí √† partir d'une s√©quence de tuples `key, item` ‚Üí tr√®s performant : 10x plus rapide que `Map` pour le _lookup_

### `Dictionary` vs `Array`

‚Üí `Array` suffit si peu de lookups (< 100) et peu d'√©l√©ments (< 100) \
‚Üí `Dictionary` sinon

## `Set` et `Map` _vs_ `IComparable`

Ne marchent que si les √©l√©ments (d'un `Set`) ou les cl√©s (d'une `Map`) sont **comparables** !

üéâ Compatibles avec tous les types F‚ôØ _(cf. √©galit√© et comparaison structurelles)_

:warning: Pour les classes : impl√©menter `IComparable` _(mais pas `IComparable<'T>`)_
