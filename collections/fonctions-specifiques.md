---
description: Fonctions propres √† un type
---

# Fonctions sp√©cifiques

## Module `Map`

Ce module comporte quelques fonctions sp√©cifiques qui sont int√©ressantes √† conna√Ætre :

### `Map.change` : modification intelligente

Signature : `Map.change key (f: 'T option -> 'T option) table`

Selon la fonction `f` pass√©e en argument, on peut : ‚Üí Ajouter, modifier ou supprimer l'√©l√©ment d'une cl√© donn√©e

| Entr√©e                                                        | Renvoie `None`                                                | Renvoie `Some newVal`                                                                     |
| ------------------------------------------------------------- | ------------------------------------------------------------- | ----------------------------------------------------------------------------------------- |
| <p><code>None</code><br><em>(√âl√©ment absent)</em></p>         | Ignore cette cl√©                                              | <p>Ajoute l'√©l√©ment <em>(key, newVal)</em><br>‚â° <code>Map.add key newVal table</code></p> |
| <p><code>Some value</code><br><em>(√âl√©ment existant)</em></p> | <p>Supprime la cl√©<br>‚â° <code>Map.remove key table</code></p> | <p>Passe la valeur √† <em>newVal</em><br>‚â° <code>Map.add key newVal table</code></p>       |

### `containsKey` _vs_ `exists` _vs_ `filter`

```
Fonction      Signature                        Commentaire                                             
------------+--------------------------------+---------------------------------------------------------
containsKey   'K -> Map<'K,'V> -> bool         Indique si la cl√© est pr√©sente                          
exists         f -> Map<'K,'V> -> bool         Indique si un couple cl√©/valeur satisfait le pr√©dicat   
filter         f -> Map<'K,'V> -> Map<'K,'V>   Conserve les couples cl√©/valeur satisfaisant le pr√©dicat

Avec pr√©dicat f: 'K -> 'V -> bool
```

```fsharp
let table = Map [ (2, "A"); (1, "B"); (3, "D") ]

table |> Map.containsKey 0  // false
table |> Map.containsKey 2  // true

let isEven i = i % 2 = 0
let isFigure (s: string) = "AEIOUY".Contains(s)

table |> Map.exists (fun k v -> (isEven k) && (isFigure v))  // true
table |> Map.filter (fun k v -> (isEven k) && (isFigure v))  // map [(2, "A")]
```

## Module `Seq`

Ce module comporte une fonction sp√©cifique tr√®s int√©ressante :

### `Seq.cache`

Le principe d'une s√©quence (aka √©num√©rable) est qu'elle est reconstruit √† chaque fois qu'elle est parcourue. Cette reconstruction peut √™tre co√ªteuse. Un algorithme parcourant (m√™me partiellement) plusieurs fois une s√©quence invariante peut √™tre optimis√© en mettant en cache cette s√©quence au moyen de la fonction `Seq.cache`.&#x20;

Signature : `Seq.cache: source: 'T seq -> 'T seq`

La mise en cache est optimis√©e en √©tant diff√©r√©e et en ne se faisant que sur les √©l√©ments parcourus.

## Type `string`

Pour manipuler les caract√®res d'une cha√Æne, plusieurs options sont possibles.

### Module `Seq`

Le type `string` impl√©mente `Seq<char>` (i.e. `System.Collections.Generic.IEnumerable<char>`). \
‚Üí On peut donc utiliser les fonctions du module `Seq`.

### Module `Array`

La m√©thode d'instance `ToCharArray()` permet de renvoyer les caract√®res d'une cha√Æne sous la forme d'un `char array`.\
‚Üí On peut donc utiliser les fonctions du module `Array`.

### Module `String`

Il existe √©galement un module `String` (venant de `FSharp.Core`) proposant quelques fonctions int√©ressantes car + performantes individuellement que celles de `Seq` et `Array` :&#x20;

‚Üí Module `(FSharp.Core.)String` (‚â† `System.String`)

‚Üí Propose quelques fonctions similaires √† celles de `Seq` en + performantes :

```fsharp
String.concat (separator: string) (strings: seq<string>) : string
String.init (count: int) (f: (index: int) -> string) : string
String.replicate (count: int) (s: string) : string

String.exists (predicate: char -> bool) (s: string) : bool
String.forall (predicate: char -> bool) (s: string) : bool
String.filter (predicate: char -> bool) (s: string) : string

String.collect (mapping:        char -> string) (s: string) : string
String.map     (mapping:        char -> char)   (s: string) : string
String.mapi    (mapping: int -> char -> char)   (s: string) : string
// Idem iter/iteri qui renvoie unit
```

**Exemples :**

```fsharp
let a = String.concat "-" ["a"; "b"; "c"]  // "a-b-c"
let b = String.init 3 (fun i -> $"#{i}")   // "#0#1#2"
let c = String.replicate 3 "0"             // "000"

let d = "abcd" |> String.exists (fun c -> c >= 'b')  // true
let e = "abcd" |> String.forall (fun c -> c >= 'b')  // false
let f = "abcd" |> String.filter (fun c -> c >= 'b')  // "bcd"

let g = "abcd" |> String.collect (fun c -> $"{c}{c}")  // "aabbccdd"

let h = "abcd" |> String.map (fun c -> (int c) + 1 |> char)  // "bcde"
```

‚òùÔ∏è **Note :** lorsque l'on a fait un `open System`, `String` repr√©sente 3 choses que le compilateur arrive √† diff√©rencier :&#x20;

* Les constructeurs `(new) String(...)`.
* `String.` donne acc√®s √† l'ensemble des fonctions du module `String`  (en _camelCase_) et des m√©thodes statiques de `System.String` (en _PascalCase_).

## Array2D

Au lieu de fonctionner avec une imbrication de collections √† N niveaux, F# propose des tableaux multidimensionnels (√©galement appel√©s matrices). En revanche, pour les cr√©er, il n'y a pas de syntaxe sp√©cifique, il faut passer par une fonction de cr√©ation. Voyons cela pour les tableaux de dimension 2.

### Type

Le type d'un tableau √† 2 dimensions est `'t[,]` mais l'IntelliSense donne parfois juste `'t array` qui est moins pr√©cis, n'indiquant plus le nombre de dimensions.

### Cr√©ation

`array2D` permet de cr√©er un tableau √† 2 dimensions √† partir d'une collection de collections ayant toutes la m√™me longueur.

`Array2D.init` permet de g√©n√©rer un tableau en sp√©cifiant : \
‚Üí Sa longueur selon les 2 dimensions N et P\
‚Üí Une fonction g√©n√©rant l'√©l√©ment aux deux indices sp√©cifi√©s.

```fsharp
let rows = [1..3]
let cols = ['A'..'C']

let cell row col = $"%c{col}%i{row}"
let rowCells row = cols |> List.map (cell row)

let list = rows |> List.map rowCells
// val arr: string list list =
//   [["A1"; "B1"; "C1"]; ["A2"; "B2"; "C2"]; ["A3"; "B3"; "C3"]]

let matrix = array2D list
// val matrix: string[,] = [["A1"; "B1"; "C1"]
//                          ["A2"; "B2"; "C2"]
//                          ["A3"; "B3"; "C3"]]

let matrix' = Array2D.init 3 3 (fun i j -> cell i cols[j])
```

### Acc√®s par indices

```fsharp
let a1  = list[0][0]    // "A1" // arr.[0].[0] avant F# 6
let a1' = matrix[0,0]  // "A1" // matrix.[0,0] avant F# 6
```

### Longueurs

`Array2D.length1` et `Array2D.length2` renvoient le nombre de lignes et de colonnes.

```fsharp
let rowCount =
    list.Length,
    list |> List.length,
    matrix |> Array2D.length1
// val rowCount: int * int * int = (3, 3, 3)

let columnCount =
    list[0] |> List.length,
    matrix |> Array2D.length2
// val columnCount: int * int = (3, 3)
```

### Slicing

Syntaxe permettant de ne prendre qu'une tranche horizontale et/ou verticale au moyen de l'op√©rateur `..`  et du caract√®re wilcard `*` pour prendre tous les indices

```fsharp
let m1 = matrix[1.., *]  // Remove first row: A2..C3
let m2 = matrix[0..1, *] // Remove last row : A1..C2
let m3 = matrix[*, 1..]  // Remove first column: B1..C3
let m4 = matrix[*, 0]    // First column: [|"A1"; "A2"; "A3"|]
                         // üí° 1 dimension array (i.e. vector)
```

Cette fonctionnalit√© offre beaucoup de souplesse. Comme pour une collection de collections, une matrice permet un acc√®s par ligne (`matrix[row, *]` vs `list[row]`). Une matrice autorise en plus un acc√®s direct par colonne (`matrix[*, col]`), ce qui n'est possible pas avec une collection de collections sans la transposer au pr√©alable - cf. fonction `transpose` ([doc](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-collections-listmodule.html#transpose)).

### Autres utilisations

```fsharp
// Mapping avec indices
let doubleNotations =
    matrix
    |> Array2D.mapi (fun row col value -> $"{value} => [R{row+1}C{col+1}]")
//   [["A1 => [R1C1]"; "B1 => [R1C2]"; "C1 => [R1C3]"]
//    ["A2 => [R2C1]"; "B2 => [R2C2]"; "C2 => [R2C3]"]
//    ["A3 => [R3C1]"; "B3 => [R3C2]"; "C3 => [R3C3]"]]

```

Autres fonctions :\
üîó [https://fsharp.github.io/fsharp-core-docs/reference/fsharp-collections-array2dmodule.html](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-collections-array2dmodule.html)
