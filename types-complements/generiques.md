# GÃ©nÃ©riques

## GÃ©nÃ©riques

Fonctions et types peuvent Ãªtre gÃ©nÃ©riques, avec + de flexibilitÃ© qu'en Câ™¯.

Par dÃ©faut, gÃ©nÃ©ricitÃ© **implicite**

* InfÃ©rÃ©e
* Voire gÃ©nÃ©ralisÃ©e, grÃ¢ce Ã  Â« gÃ©nÃ©ralisation automatique Â»

Sinon, gÃ©nÃ©ricitÃ© peut Ãªtre explicite ou rÃ©solue statiquement.

:warning: Notations diffÃ©rentes _(avant F# 7)_ :

* `'T` : paramÃ¨tre de type gÃ©nÃ©rique
* `^T` : paramÃ¨tre de type rÃ©solu statiquement _(SRTP)_
* Depuis F# 7, `'T`  peut dÃ©signer l'un ou l'autre

## GÃ©nÃ©ricitÃ© implicite

Le compilateur est capable d'infÃ©rer qu'une fonction est gÃ©nÃ©rique.\
â†’ Simplifie le code

```fsharp
module ListHelper =
    let singleton x = [x]
    // val singleton : x:'a -> 'a list

    let couple x y = [x; y]
    // val couple : x:'a -> y:'a -> 'a list
```

ğŸ‘‰ **Explications :**

* `singleton` : `x` est juste mis dans une liste gÃ©nÃ©rique â†’ son type est donc quelconque
* `couple` : ses 2 arguments `x` et `y` doivent Ãªtre du mÃªme type pour pouvoir Ãªtre dans une liste

â˜ï¸ Les noms des types gÃ©nÃ©riques infÃ©rÃ©s rendent parfois une signature de fonction difficile Ã  comprendre. Ne pas hÃ©siter Ã  ajouter alors des annotations de types plus explicites.\
Exemple: `Result<'a, 'b>` â†’ `Result<'ok, 'err>`

## GÃ©nÃ©ricitÃ© explicite

```fsharp
let print2 x y = printfn "%A, %A" x y
// val print2 : x:'a -> y:'b -> unit
```

â†’ InfÃ©rence de la gÃ©nÃ©ricitÃ© de `x` et `y` ğŸ‘

â“ **Comment indiquer que `x` et `y` doivent avoir le mÃªme type ?**

â†’ Besoin de l'indiquer explicitement :

```fsharp
let print2<'T> (x: 'T) (y: 'T) = printfn "%A, %A" x y
// val print2 : x:'T -> y:'T -> unit
```

### GÃ©nÃ©ricitÃ© explicite - Forme inline

ğŸ’¡ **Astuce :** la convention en `'x` permet ici d'Ãªtre + concis :

```fsharp
// AVANT
let print2<'T> (x: 'T) (y: 'T) = printfn "%A, %A" x y

// APRES
let print2 (x: 'T) (y: 'T) = printfn "%A, %A" x y
```

### GÃ©nÃ©ricitÃ© explicite - Type

La dÃ©finition des types gÃ©nÃ©riques est explicite :

```fsharp
type Pair = { Item1: 'T ; Item2: 'T }
// ğŸ’¥                ~~          ~~
// Error FS0039: Le paramÃ¨tre de type `'T` n'est pas dÃ©fini.

// âœ… Records et unions avec 1 ou 2 paramÃ¨tres de type
type Pair<'T> = { Item1: 'T; Item2: 'T }

type Tuple<'T, 'U> = { Item1: 'T; Item2: 'U }

type Option<'T> = None | Some of 'T

type Result<'TOk, 'TErr> =
    | Ok of 'TOk
    | Error of 'TErr
```

## GÃ©nÃ©ricitÃ© ignorÃ©e

Le _wildcard_ `_` permet de remplacer un paramÃ¨tre de type ignorÃ© :

```fsharp
let printSequence (sequence: seq<'T>) = sequence |> Seq.iteri (printfn "%i: %A")
// Versus
let printSequence (sequence: seq<_>) = ...
```

Encore + utile avec type flexibleğŸ“[types-flexibles.md](types-flexibles.md "mention") :

```fsharp
let tap action (sequence: 'TSeq when 'TSeq :> seq<_>) =
    sequence |> Seq.iteri action
    sequence
// action:(int -> 'a -> unit) -> sequence:'TSeq -> 'TSeq when 'TSeq :> seq<'a>

// Versus
let tap action (sequence: #seq<_>) = ...
```

## SRTP

Fâ™¯ propose deux catÃ©gories de types de paramÃ¨tre :

* `'X` : type de paramÃ¨tre gÃ©nÃ©rique comme en C# : le type concret est dÃ©fini au runtime.
* `^X` : type de paramÃ¨tre rÃ©solu statiquement : le type concret est dÃ©fini lors de la compilation.

â˜ **SRTP :** abrÃ©viation frÃ©quente de _Statically Resolved Type Parameter_

### SRTP : pourquoi ?

Sans SRTP :

```fsharp
let add x y = x + y
// val add : x:int -> y:int -> int
```

â†’ InfÃ©rence du type `int` pour `x` et `y`, sans gÃ©nÃ©ralisation (aux `float` par ex.) !

Avec SRTP, de pair avec fonction `inline` :

```fsharp
let inline add x y = x + y
// val inline add : x: ^a -> y: ^b -> ^c
//    when ( ^a or ^b ) : (static member (+) : ^a * ^b -> ^c)
//    â˜ Contrainte de membre ğŸ“

let x = add 1 2       // âœ… val x: int = 3
let y = add 1.0 2.0   // âœ… val y: float = 3.0
```

### SRTP : duck typing

On peut utiliser les SRTP pour appeler un membre en mÃªme temps que l'on contraint son existence dans le type associÃ©.

#### Duck typing d'une propriÃ©tÃ©

```fsharp
// -- DÃ©claration --
let inline Length x = (^a : (member Length : _) x)
// FSI: val inline Length: x:  ^a -> 'a0 when  ^a: (member get_Length:  ^a -> 'a0)

// -- Usages --
let textLength = "text" |> Length
// FSI: val textLength: int = 4

let listLength = [ 1..10 ] |> Length
// FSI: val listLength: int = 10
```

ğŸ’¡ Dans vscode avec Ionide, l'IntelliSense est plus friendly :&#x20;

```
val inline Length:
   x: 'a (requires member Length )
   -> 'a
```

#### Duck typing d'une mÃ©thode

DÃ©claration :&#x20;

```fsharp
let inline ToString format x = (^x : (member ToString : ^format -> string) (x, format))
(*
val inline ToString:
  format:  ^format -> x:  ^x -> string
    when  ^x: (member ToString:  ^x *  ^format -> string)
*)
```

ğŸ’¡ IntelliSense dans VsCode + Ionide :&#x20;

```
val inline ToString:
   format: 'format ->
   x     : 'x      (requires member ToString )
        -> string
```

Usages (exemples) :&#x20;

```fsharp
1 |> ToString "c" // "1,00 â‚¬", or "Â£1.00"... (selon paramÃ¨tres locaux)

open System
DateTime.Now |> ToString "s" // "2023-02-24T14:04:37"
DateTime.Now |> ToString Globalization.CultureInfo.CurrentCulture // "24/02/2023 14:04:37"
```

#### Duck typing : complÃ©ments

ğŸ’¡ Plus d'informations et de conseils sur le duck typing dans l'article ci-dessous duquel sont extraits certains exemples :&#x20;

{% embed url="https://www.compositional-it.com/news-blog/static-duck-typing-in-f/" %}

Un autre exemple de SRTP est dÃ©taillÃ© dans cette [rÃ©ponse sous StackOverflow](https://stackoverflow.com/a/72088241/8634147) Ã  propos du type `Functor` dans la librairie [FSharpPlus](https://github.com/fsprojects/FSharpPlus).

### SRTP en Fâ™¯ 7.0

* Plusieurs amÃ©liorations ont Ã©tÃ© introduites en [F# 7.0](https://devblogs.microsoft.com/dotnet/announcing-fsharp-7/#making-working-with-srtps-easier) pour amÃ©liorer la syntaxe des SRTP.
* Jusqu'en Fâ™¯ 6.0, il fallait mettre un espace entre le chevron ouvrant et le SRTP.
* Depuis Fâ™¯ 7.0 (novembre 2022), cela n'est pas nÃ©cessaire. Cela permet d'Ãªtre uniforme avec les types gÃ©nÃ©riques :

```fsharp
// Avant F# 7.0
type C< ^T> = class end
let f< ^T> (x:^T) = ()

// Depuis F# 7.0
type C<^T> = class end
let f<^T> (x:^T) = ()
```

### SRTP : recommandation

* Plusieurs amÃ©liorations ont Ã©tÃ© introduites en [F# 7.0](https://devblogs.microsoft.com/dotnet/announcing-fsharp-7/#making-working-with-srtps-easier) pour amÃ©liorer la syntaxe des SRTP. Elles rendent le code plus lisible.
* Cependant, la syntaxe reste encore un peu difficile Ã  lire, Ã  dessein : pour encourager les solutions alternatives.
* De plus, on ne peut pas savoir Ã  l'avance tous les types compatibles avec une fonction avec SRTP.
* Enfin, cela peut ralentir beaucoup la compilation. C'est un des problÃ¨mes remontÃ©s par des utilisateurs de la librairie FSharpPlus.

> ğŸ‘‰ Les SRTP sont Ã  utiliser avec parcimonie car leur syntaxe est difficile Ã  lire.

## Contraintes

Les contraintes sur paramÃ¨tres de type en Fâ™¯ reposent sur le mÃªme principe qu'en Câ™¯, avec quelques diffÃ©rences :&#x20;

| Contrainte  | Syntaxe Fâ™¯                     | Syntaxe Câ™¯                      |
| ----------- | ------------------------------ | ------------------------------- |
| Mots clÃ©s   | `when xxx and yyy`             | `where xxx, yyy`                |
| Emplacement | Juste aprÃ¨s type :             | Fin de ligne :                  |
|             | `fn (arg: 'T when 'T ...)`     | `Method<T>(arg: T) where T ...` |
|             | Dans chevrons :                |                                 |
|             | `fn<'T when 'T ...> (arg: 'T)` |                                 |

### Vue d'ensemble

| Contrainte              | Syntaxe Fâ™¯               | Syntaxe Câ™¯                 |
| ----------------------- | ------------------------ | -------------------------- |
| Type de base            | `'T :> my-base`          | `T : my-base`              |
| Type valeur             | `'T : struct`            | `T : struct`               |
| Type rÃ©fÃ©rence          | `'T : not struct`        | `T : class`                |
| Type rÃ©fÃ©rence nullable | `'T : null`              | `T : class?`               |
| Constructeur sans param | `'T : (new: unit -> 'T)` | `T : new()`                |
| Ã‰numÃ©ration             | `'T : enum<my-enum>`     | `T : System.Enum`          |
| Comparaison             | `'T : comparison`        | â‰ƒ `T : System.IComparable` |
| Ã‰galitÃ©                 | `'T : equality`          | _(pas nÃ©cessaire)_         |
| Membre explicite        | `^T : member-signature`  | _(pas d'Ã©quivalent)_       |

### Contraintes de type

Pour forcer le type de base : classe mÃ¨re ou interface

```fsharp
let check<'error' when 'error' :> System.Exception> condition (error: 'error') =
    if not condition then raise error
```

â†’ Ã‰quivalent en Câ™¯ :

```csharp
static void check<TError>(bool condition, TError error) where TError : System.Exception
{
    if (!condition) throw error;
}
```

ğŸ’¡ Syntaxe alternative : `let check condition (error: #System.Exception)` \
â†’ Cf. [types-flexibles.md](types-flexibles.md "mention") ğŸ“

### Contrainte de nullabilitÃ©

Exemple de fonction avec une telle contrainte : la fonction `Option.ofObj` ([type-option.md](../types-monadiques/type-option.md "mention")ğŸ“) prend en entrÃ©e une valeur nullable venant "de l'extÃ©rieur" et la convertit en type `Option` plus sÃ»r Ã  utiliser.

```fsharp
module Option =
    let ofObj value = // 'a -> 'a option when 'a: null
        match value with
        | null -> None
        | _ -> Some value
```

ğŸ‘‰ Le paramÃ¨tre gÃ©nÃ©rique `'a` comporte une contrainte de nullabilitÃ© : `when 'a: null`.

:warning: **Attention :** ne pas confondre avec le type `System.Nullable<T>` qui est un type valeur alors que la contrainte de nullabilitÃ© ne s'applique Ã  qu'un type rÃ©fÃ©rence.

â˜ï¸ **Note :** cette contrainte ne s'applique pas aux types Fâ™¯ (Tuple, Record, Union) qui sont bien des types rÃ©fÃ©rence mais qui ne peuvent pas Ãªtre instanciÃ©s `null` dans les cas d'usage standard. Cependant, lors d'une interop avec une librairie .NET telle que le micro-ORM Dapper, on peut obtenir une valeur `null` Ã  la barbe du compilateur Fâ™¯. Pour retomber sur nos pieds, on peut :&#x20;

* Soit dÃ©corer le type avec un attribut `AllowNullLiteral`, mais on perd alors la sÃ©curitÃ© des types non nullables.
* Soit utiliser la fonction `Unchecked.defaultof` pour tester cette nullitÃ© sans perdre en sÃ©curitÃ© dans le reste de la codebase :&#x20;

```fsharp
module Option =
    let private isHiddenNull (arg: 'a when 'a: not struct) =
        arg = Unchecked.defaultof<'a> // â˜ï¸ Ajoute contrainte `'when a: equality`

    let ofHiddenNullable value =
        if value |> isHiddenNull
        then None
        else Some value
```

### Contrainte d'enum

```fsharp
open System

let getValues<'T when 'T : enum<int>>() =
    Enum.GetValues(typeof<'T>) :?> 'T array

type ColorEnum = Red = 1 | Blue = 2
type ColorUnion = Red | Blue

let x = getValues<ColorEnum>()   // [| Red; Blue |]
let y = getValues<ColorUnion>()  // ğŸ’¥ Exception ou erreur de compilation (1)
```

(1) La contrainte `when 'T : enum<int>` permet :&#x20;

* D'Ã©viter la `ArgumentException` au runtime _(Type provided must be an Enum)_&#x20;
* Au profit d'une erreur dÃ¨s la compilation _(The type 'ColorUnion' is not an enum)_

### Contrainte de comparaison

Syntaxe : `'T : comparison`

Indique que le type `'T` doit :&#x20;

* soit implÃ©menter `IComparable` (1)&#x20;
* soit Ãªtre un collection d'Ã©lÃ©ments comparables (2)

â˜ **Notes :**

1. `'T : comparison` > `'T : IComparable` â—
2. `'T : comparison` â‰  `'T : IComparable<'T>` â—
3. Pratique pour mÃ©thodes gÃ©nÃ©riques `compare` ou `sort` ğŸ’¡

**Exemple :**&#x20;

```fsharp
let compare (x: 'T) (y: 'T when 'T : comparison) =
    if   x < y then -1
    elif x > y then +1
    else 0

// Comparaison de nombres et de chaÃ®nes
let x = compare 1.0 2.0  // -1
let y = compare "a" "A"  // +1

// Comparaison de listes d'entier
let z = compare [ 1; 2; 3 ] [ 2; 3; 1 ]  // -1

// Comparaison de listes de fonctions
let a = compare [ id; ignore ] [ id; ignore ]
// ğŸ’¥             ~~
// error FS0001: Le type '('a -> 'a)' ne prend pas en charge la contrainte 'comparison'.
// Par exemple, il ne prend pas en charge l'interface 'System.IComparable'
```

### Contrainte de membre explicite

> **Pb :** Comment indiquer qu'un objet doit disposer d'un certain membre ?

â€¢ ManiÃ¨re classique en .NET : typage nominal \
â†’ Contrainte spÃ©cifiant type de base (interface ou classe parent)

â€¢ Alternative en Fâ™¯ : typage structurel _(a.k.a duck-typing des langages dynamiques)_ \
â†’ Contrainte de membre explicite \
â†’ UtilisÃ©e avec les SRTP _(statically resolved type parameter)_

```fsharp
let inline add (value1 : ^T when ^T : (static member (+) : ^T * ^T -> ^T), value2: ^T) =
    value1 + value2

let x = add (1, 2)
// val x : int = 3
let y = add (1.0, 2.0)
// val y : float = 3.0
```

âš–ï¸ Pour et contre :

* ğŸ‘ Permet de rendre code gÃ©nÃ©rique pour types hÃ©tÃ©rogÃ¨nes
* ğŸ‘ Difficile Ã  lire, Ã  maintenir. Ralentit la compilation
* ğŸ‘‰ Ã€ utiliser dans une librairie, pas pour modÃ©liser un domaine
