# Conversion

## Conversion de nombre

Types num√©riques :

* Entier : `byte`, `int16`, `int`/`int32`, `int64`
* Flottant : `float`/`double` (64b), `single` (32b), `decimal`
* Autres : `char`, `enum`

Conversion entre eux **explicite** ‚Üí Fonction de m√™me nom que le type cible

```fsharp
let x = 1               // val x : int = 1
let y = float x         // val y : float = 1.0
let z = decimal 1.2     // val z : decimal = 1.2M
let s = char 160        // val s : char = ' '
```

## Conversion entre nombre et enum

Il faut utiliser le nom de l'enum pour convertir un nombre en enum :

* Soit en param√®tre g√©n√©rique de la fonction `enum<my-enum>` ‚ë†
* Soit par annotation de type et la fonction `enum` sans param√®tre g√©n√©rique ‚ë°

L'op√©ration inverse utilise la fonction `int` ‚ë¢

```fsharp
type Color =
    | Red   = 1
    | Green = 2
    | Blue  = 3

let color1 = enum<Color> 1      // (1)  val color1 : Color = Red
let color2 : Color = enum 2     // (2)  val color2 : Color = Green
let value3 = int Color.Blue     // (3)  val c1 : int = 3
```

## Casting d'objets

‚Üí S'utilise pour un objet dont le type appartient √† une hi√©rarchie

| Fonctionnalit√© | Pr√©cision             | S√ªr        | Op√©rateur | Fonction   |
| -------------- | --------------------- | ---------- | --------- | ---------- |
| _Upcast_       | Vers type de base     | ‚úÖ Oui      | `:>`      | `upcast`   |
| _Downcast_     | Vers type d√©riv√©      | ‚ùå Non (\*) | `:?>`     | `downcast` |
| Test de type   | Dans pattern matching | ‚úÖ Oui      | `:?`      |            |

(\*) Le _downcast_ peut √©chouer ‚Üí risque de `InvalidCastException` au runtime ‚ö†Ô∏è

## Upcasting d'objets

En C‚ôØ : _upcast_ peut g√©n√©ralement √™tre implicite

```csharp
object o = "abc";
```

En F‚ôØ : _upcast_ peut parfois √™tre implicite mais en g√©n√©ral doit √™tre **explicite**, avec op√©rateur `:>`

```fsharp
let o1: obj = "abc"             // Implicite üí• Error FS0001...
let o2 = "abc" :> obj           // Explicite üëå

let toObject x : obj = x        // obj -> obj
let o3 = "abc" |> toObject      // Implicite üëå

let l1: obj list = [1; 2; 3]    // Implicite üëå
let l2: int seq = [1; 2; 3]     // Implicite üí• Error FS0001...
```

‚òùÔ∏è R√®gles √©largies/assouplies en F‚ôØ 6\
‚Üí Exemple : upcast implicite de `int list` vers `int seq`

```fsharp
let l2: int seq = [1; 2; 3]  // üëå OK en F‚ôØ 6
```

## Casting d'objets - Exemple

```fsharp
type Base() =
    abstract member F : unit -> string
    default _.F() = "F Base"

type Derived1() =
    inherit Base()
    override _.F() = "F Derived1"

type Derived2() =
    inherit Base()
    override _.F() = "F Derived2"

let d1 = Derived1()
let b1 = d1 :> Base         // val b1 : Base
let b1': Base = upcast d1   // val b1' : Base

let t1 = b1.GetType().Name  // val t1 : string = "Derived1"

let one = box 1  // val one : obj = 1

let d1' = b1 :?> Derived1           // val d1' : Derived1
let d2' = b1 :?> Derived2           // üí• System.InvalidCastException

let d1'': Derived1 = downcast b1    // val d1'' : Derived1

let f (b: Base) =
    match b with
    | :? Derived1 as derived1 -> derived1.F()
    | :? Derived2 as derived2 -> derived2.F()
    | _ -> b.F()

let x = f b1            // val x : string = "F Derived1"
let y = b1.F()          // val y : string = "F Derived1"
let z = f (Base())      // val z : string = "F Base"
let a = f (Derived2())  // val a : string = "F Derived2"
// ‚òùÔ∏è      ^^^^^^^^  Upcast implicite
```

## Test de type

L'op√©rateur `:?` r√©alise un test de type et renvoie un bool√©en.

```fsharp
let isDerived1 = b1 :? Derived1   // val isDerived1 : bool = true
let isDerived2 = b1 :? Derived2   // val isDerived2 : bool = false
```

‚òùÔ∏è Il faut _boxer_ un nombre pour tester son type :

```fsharp
let isIntKo = 1 :? int          // üí• Error FS0016
let isInt32 = (box 1) :? int    // val isInt32 : bool = true
let isFloat = (box 1) :? float  // val isFloat : bool = false
```

üí° `box x` ‚âÉ `x :> obj`
