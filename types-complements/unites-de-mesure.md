# UnitÃ©s de mesure

## PrÃ©sentation

Moyen d'associer un **type numÃ©rique** Ã  une unitÃ© de mesure

* DurÃ©e : `s` _aka_ `second`
* Masse : `kg`
* Longueur : `m` _aka_ `metre`
* ...

Les unitÃ©s sont **vÃ©rifiÃ©es Ã  la compilation**

* EmpÃªche d'ajouter des ğŸ¥¦ Ã  des ğŸ¥• â†’ **code + sÃ»r**
* Permet de les **combiner** : Vitesse = Distance / DurÃ©e â†’ `m/s`

## DÃ©claration

Syntaxe basÃ©e sur attribut `[<Measure>]`

```fsharp
// ğŸ‘‰ Nouvelles unitÃ©s "from scratch"
[<Measure>] type kilogram
[<Measure>] type metre
[<Measure>] type second

// ğŸ‘‰ Alias d'unitÃ©s existantes
[<Measure>] type kg = kilogram
[<Measure>] type m  = metre
[<Measure>] type s  = second

// ğŸ‘‰ Combinaison d'unitÃ©s existantes
[<Measure>] type Hz = / s
[<Measure>] type N = kg m / s^2
```

## SI

Les unitÃ©s du **SystÃ¨me International** sont prÃ©dÃ©finies dans les namespaces :

`FSharp.Data.UnitSystems.SI.UnitNames` :

* `ampere`, `hertz`, `joule`, `kelvin`, `kilogram`, `metre`...
* https://fsharp.github.io/fsharp-core-docs/reference/fsharp-data-unitsystems-si-unitnames.html

`FSharp.Data.UnitSystems.SI.UnitSymbols`

* `A`, `Hz`, `J`, `K`, `kg`, `m`...
* https://fsharp.github.io/fsharp-core-docs/reference/fsharp-data-unitsystems-si-unitsymbols.html

## Usage

```fsharp
// UnitÃ© dÃ©finie en annotant le nombre
let distance = 1.0<m>               // val distance : float<m> = 1.0
let time = 2.0<s>                   // val time : float<s> = 2.0

// UnitÃ© combinÃ©e, infÃ©rÃ©e
let speed = distance / time         // val speed : float<m/s> = 0.5

// UnitÃ© combinÃ©e, dÃ©finie par annotation
let [<Literal>] G = 9.806<m/s^2>    // val G : float<m/s ^ 2> = 9.806

// Comparaison
let sameFrequency = (1<Hz> = 1</s>)   // âœ… true
let ko1 = (distance = 1.0)            // âŒ Error FS0001: IncompatibilitÃ© de type.
                                      // ğŸ’¥ Attente de 'float<m>' mais obtention de 'float'
let ko2 = (distance = 1<m>)           // ğŸ’¥ Attente de 'float<m>' mais obtention de 'int<m>'
let ko3 = (distance = time)           // ğŸ’¥ Attente de 'float<m>' mais obtention de 'float<s>'
```

## Symbole

ğŸ’¡ **Astuce :** utilisation des _doubles back ticks_

```fsharp
[<Measure>] type ``Î©``
[<Measure>] type ``Â°C``
[<Measure>] type ``Â°F``

let waterFreezingAt = 0.0<``Â°C``>
// val waterFreezingAt : float<Â°C> = 0.0

let waterBoilingAt = 100.0<``Â°C``>
// val waterBoilingAt : float<Â°C> = 100.0
```

## Conversion

* Facteur multiplicatif avec une unitÃ© `<target/source>`
* Fonction de conversion utilisant ce facteur

```fsharp
[<Measure>] type m
[<Measure>] type cm
[<Measure>] type km

module Distance =
    let toCentimeter (x: float<m>) = // (x: float<m>) -> float<cm>
        x * 100.0<cm/m>

    let toKilometer (x: float<m>) = // (x: float<m>) -> float<km>
        x / 1000.0<m/km>

let a = Distance.toCentimeter 1.0<m>   // val a : float<cm> = 100.0
let b = Distance.toKilometer 500.0<m>  // val b : float<km> = 0.5
```

Exemple 2 : degrÃ© Celsius (Â°C) â†’ degrÃ© Fahrenheit (Â°F)

```fsharp
[<Measure>] type ``Â°C``
[<Measure>] type ``Â°F``

module Temperature =
    let toFahrenheit ( x: float<``Â°C``> ) =  // (x: float<Â°C>) -> float<Â°F>
        9.0<``Â°F``> / 5.0<``Â°C``> * x + 32.0<``Â°F``>

let waterFreezingAt = Temperature.toFahrenheit 0.0<``Â°C``>
// val waterFreezingAt : float<Â°F> = 32.0

let waterBoilingAt = Temperature.toFahrenheit 100.0<``Â°C``>
// val waterBoilingAt : float<Â°F> = 212.0
```

## Ajouter/supprimer

Ajouter une unitÃ© Ã  un nombre nu :

* âœ… `number * 1.0<target>`

Supprimer l'unitÃ© d'un nombre `number : float<source>` :

* âœ… `number / 1.0<source>`
* âœ… `float number`

CrÃ©er une liste de nombres avec unitÃ© :

* âœ… `[1<m>; 2<m>; 3<m>]`
* âŒ `[1<m>..3<m>]` _(un range nÃ©cessite des nombres nus)_
* âœ… `[ for i in [1..3] -> i * 1<m> ]`

## EffacÃ©es au runtime !

Les unitÃ©s de mesure sont propres au compilateur Fâ™¯. â†’ Elles ne sont pas compilÃ©es en .NET

## Type avec unitÃ© gÃ©nÃ©rique

Besoin de distinguer d'un type gÃ©nÃ©rique classique â†’ Annoter l'unitÃ© gÃ©nÃ©rique avec `[<Measure>]`

```fsharp
type Point<[<Measure>] 'u, 'data> =
    { X: float<'u>; Y: float<'u>; Data: 'data }

let point = { X = 10.0<m>; Y = 2.0<m>; Data = "abc" }
// val point : Point<m, string> = { X = 10.0; Y = 2.0; Data = "abc" }
```

## UnitÃ© pour primitive non numÃ©rique

ğŸ’¡ Nuget [FSharp.UMX](https://github.com/fsprojects/FSharp.UMX) _(Unit of Measure Extension)_ \
â†’ Pour autres primitives `bool`, `DateTime`, `Guid`, `string`, `TimeSpan`

```fsharp
open System

#r "nuget: FSharp.UMX"
open FSharp.UMX

[<Measure>] type ClientId
[<Measure>] type OrderId

type Order = { Id: Guid<OrderId>; ClientId: string<ClientId> }

let order = { Id = % Guid.NewGuid(); ClientId = % "RDE" }
```
