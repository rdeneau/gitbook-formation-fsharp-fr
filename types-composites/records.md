# Records

## Points clÃ©s

Type produit, alternative au Tuple quand type imprÃ©cis sous forme de tuple

* Exemple : `float * float`
  * Point dans un plan ?
  * CoordonnÃ©es gÃ©ographiques ?
  * Parties rÃ©elle et imaginaire d'un nombre complexe ?
* _Record_ permet de lever le doute en nommant le type et ses Ã©lÃ©ments

```fsharp
type Point = { X: float; Y: float }
type Coordinate = { Latitude: float; Longitude: float }
type ComplexNumber = { Real: float; Imaginary: float }
```

## DÃ©claration

* Membres nommÃ©s en PascalCase, pas en ~~camelCase~~
* Membres sÃ©parÃ©s `;` ou retours Ã  la ligne
* Saut de ligne aprÃ¨s `{` qu'en cas de membre additionnels

```fsharp
type PostalAddress =        â”†     type PostalAddress =
    { Address: string       â”†         {
      City: string          â”†             Address: string
      Zip: string }         â”†             City: string
                            â”†             Zip: string
                            â”†         }
                            â”†         member x.ZipAndCity = $"{x.Zip} {x.City}"
```

ğŸ”— [https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/formatting](https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/formatting)

* [#use-pascalcase-for-type-declarations-members-and-labels](https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/formatting#use-pascalcase-for-type-declarations-members-and-labels)
* [#formatting-record-declarations](https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/formatting#formatting-record-declarations)

## Instanciation

* MÃªme syntaxe qu'un objet anonyme Câ™¯ sans `new`
  * Mais le record doit avoir Ã©tÃ© dÃ©clarÃ© au-dessus !
* Membres peuvent Ãªtre renseignÃ©s dans n'importe quel ordre
  * Mais doivent tous Ãªtre renseignÃ©s â†’ pas de membres optionnels !

```fsharp
type Point = { X: float; Y: float }

let point1  = { X = 1.0; Y = 2.0 }
let point2  = { Y = 2.0; X = 1.0 }   // ğŸ‘ˆ Possible mais confusant dans ce cas
let pointKo = { Y = 2.0 }            // ğŸ’¥ Error FS0764:
//            ~~~~~~~~~~~ Aucune assignation spÃ©cifiÃ©e pour le champ 'X' de type 'Point'
```

:warning: **PiÃ¨ge :** cette syntaxe est similaire mais pas identique Ã  celle de la dÃ©claration\
â†’ `:` pour dÃ©finir le type du membre\
â†’ `=` pour dÃ©finir la valeur du membre

Les instances "longues" devraient Ãªtre Ã©crites sur plusieurs lignes :

```fsharp
// Style 1 : le + classique
let rainbow =
    { Boss = "Jeffrey"
      Lackeys = ["Zippy"; "George"; "Bungle"] }

// Style 2 : similaire au C#
// â†’ Plus facile de rÃ©-indenter et rÃ©-ordonner (par ex. Lackeys avant Boss)
let rainbow =
    {
        Boss = "Jeffrey"
        Lackeys = ["Zippy"; "George"; "Bungle"]
    }
```

## DÃ©construction

MÃªme syntaxe pour dÃ©construire un _Record_ que pour l'instancier :

```fsharp
let { X = x1 } = point1; // (1)
let { X = x2; Y = y2 } = point1;

// On peut aussi accÃ©der aux membres via le point '.'
let x3 = point1.X;
let y3 = point1.Y;
```

ğŸ’¡ On peut ignorer certains champs\
â†’ Rend explicite les champs utilisÃ©s ğŸ‘

:warning: On ne peut pas dÃ©construire les membres additionnels tels que les propriÃ©tÃ©s !

```fsharp
type PostalAddress =
    {
        Address: string
        City: string
        Zip: string
    }
    member x.CityLine = $"{x.Zip} {x.City}"

let address = { Address = ""; City = "Paris"; Zip = "75001" }

let { CityLine = cityLine } = address   // ğŸ’¥ Error FS0039
//    ~~~~~~~~ L'Ã©tiquette d'enregistrement 'CityLine' n'est pas dÃ©finie
let cityLine = address.CityLine         // ğŸ‘Œ OK
```

## InfÃ©rence

* L'infÃ©rence de type ne marche pas quand on _"dot"_ une `string`
* ... mais elle marche avec un _Record_ ?!

```fsharp
type PostalAddress =
    { Address: string
      City   : string
      Zip    : string }

let department address =
    address.Zip.Substring(0, 2) |> int
    //     ^^^^ ğŸ’¡ Permet d'infÃ©rer que address est de type `PostalAddress`

let departmentKo zip =
    zip.Substring(0, 2) |> int
//  ~~~~~~~~~~~~~ Error FS0072
```

## Pattern matching

Fonction `inhabitantOf` donnant le nom des habitants Ã  une adresse :

```fsharp
type Address = { Street: string; City: string; Zip: string }

let department { Zip = zip } = zip.Substring(0, 2) |> int

let inIleDeFrance departmentNumber =
    [ 75; 77; 78 ] @ [ 91..95 ] |> List.contains departmentNumber

let inhabitantOf address =
    match address with
    | { Street = "PÃ´le"; City = "Nord" } -> "PÃ¨re NoÃ«l"
    | { City = "Paris" } -> "Parisien"
    | _ when department address = 78 -> "Yvelinois"
    | _ when department address |> inIleDeFrance -> "Francilien"
    | _ -> "FranÃ§ais"   // Le discard '_' sert de pattern par dÃ©faut (obligatoire)
```

## Conflit de noms

En Fâ™¯, le typage est nominal et non pas structurel comme en TypeScript.\
â†’ On peut Ã©crire plusieurs fois le mÃªme Record avec les mÃªmes champs \
&#xNAN;_(cf. `First` et `Last` ci-dessous)_.\
â†’ Il faut qualifier un champ avec le nom du Record pour lever l'ambiguÃ¯tÃ© sur le type.

```fsharp
type Person1 = { First: string; Last: string }
type Person2 = { First: string; Last: string }
let alice = { First = "Alice"; Last = "Jones"}  // val alice: Person2...
// (car Person2 est le type le + proche qui correspond aux Ã©tiquettes First et Last)

// âš ï¸ DÃ©construction
let { First = firstName } = alice   // Warning FS0667
//  ~~~~~~~~~~~~~~~~~~~~~  Les Ã©tiquettes et le type attendu du champ de ce Record
//                         ne dÃ©terminent pas de maniÃ¨re unique un type Record correspondant

let { Person2.Last = lastName } = alice     // ğŸ‘Œ OK avec type en prÃ©fixe
let { Person1.Last = lastName } = alice     // ğŸ’¥ Error FS0001
//                                ~~~~~ Type 'Person1' attendu, 'Person2' reÃ§u
```

â˜ **Conseil :** mieux vaut Ã©crire des types distincts ou les sÃ©parer dans â‰  modules.

## Modification

Record immuable mais facile de crÃ©er nouvelle instance ou copie modifiÃ©e

* Expression de _**copy and update**_ d'un _Record_
* Syntaxe spÃ©ciale pour ne modifier que certains champs
* Multi-lignes si expression longue

```fsharp
let address2 = { address with Street = "Rue Vivienne" }

let { City = city; Zip = zip } = address
let address2' = { Street = "Rue Vivienne"; City = city; Zip = zip }
// address2 = address2'

let address3 =                  â”†      let address3' =
    { address with              â”†          { Street = address.Street
        City = "Lyon"           â”†            City   = "Lyon"
        Zip  = "69001" }        â”†            Zip    = "69001" }
// address3 = address3'
```

### DiffÃ©rences Câ™¯ / Fâ™¯ / JS

```csharp
// Record Câ™¯ 9.0
address with { Street = "Rue Vivienne" }
```

```fsharp
// Fâ™¯ copy and update
{ address with Street = "Rue Vivienne" }
```

```typescript
// Object destructuring with spread operator
{ ...address, street: "Rue Vivienne" }
```

### Limites ğŸ›‘

LisibilitÃ© rÃ©duite quand plusieurs niveaux imbriquÃ©s

```fsharp
type Street = { Num: string; Label: string }
type Address = { Street: Street }
type Person = { Address: Address }

let person = { Address = { Street = { Num = "15"; Label = "rue Neuf" } } }

let person' =
    { person with
        Address =
          { person.Address with
              Street =
                { person.Address.Street with
                    Num = person.Address.Street.Num + " bis" } } }
```

## Record `struct`

Attribut `[<Struct>]` permet de passer d'un type rÃ©fÃ©rence Ã  un type valeur :

```fsharp
[<Struct>]
type Point = { X: float; Y: float; Z: float }
```

âš–ï¸ **Pros/Cons d'une `struct` :**

* âœ… Performant car ne nÃ©cessite pas de _garbage collection_
* :warning: PassÃ©e par valeur â†’ pression sur la mÃ©moire

ğŸ‘‰ AdaptÃ© Ã  un type "petit" en mÃ©moire (\~2-3 champs)
