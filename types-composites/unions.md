---
description: A.k.a. Discriminated Unions (DU)
---

# Unions

## Points clÃ©s

* Terme exacte : Â« Union discriminÃ©e Â», _Discriminated Union (DU)_
* Types Somme : reprÃ©sente un **OU**, un **choix** entre plusieurs _Cases_
  * MÃªme principe que pour une `enum` mais gÃ©nÃ©ralisÃ©
* Chaque _case_ doit avoir un _Tag_ _(a.k.a Label)_
  * C'est le **discriminant** de l'union pour identifier le _case_
* Chaque _case_ **peut** contenir des donnÃ©es

```fsharp
type Billet =
    | Adulte                 // aucune donnÃ©e -> â‰ƒ singleton stateless
    | Senior of int          // contient un 'int' (mais on ne sait pas ce que c'est)
    | Enfant of age: int     // contient un 'int' de nom 'age'
    | Famille of Billet list // contient une liste de billet
                             // type rÃ©cursif -- pas besoin de 'rec'
```

### Qualification des _Tags_

Les _Tags_ peuvent Ãªtre utilisÃ©s :&#x20;

* sans qualification â†’ `Adulte`
* sauf pour rÃ©soudre un conflit de noms ou par choix de nommage â†’ `Billet.Adulte`

â˜ On peut forcer l'usage avec qualification en dÃ©corant l'union de l'attribut `RequireQualifiedAccess`, essentiellement pour des raisons de nommage de l'union et de ses tags, pour que le code se lise sans ambiguÃ¯tÃ©.

### Casse des _Tags_

Les _Tags_ doivent Ãªtre nommÃ©s en PascalCase â—

ğŸ’¡ Depuis F# 7.0, la camelCase est possible si l'union est dÃ©corÃ©e avec  `RequireQualifiedAccess.`

### Champs nommÃ©s - _Labelled Fields_

Pratiques pour :&#x20;

* Ajouter un sens Ã  un type primitif : \
  â†’ Dans l'exemple prÃ©cÃ©dent, en ligne 4, le _case_ `Enfant` contient un champ de type `int` qui est nommÃ© `age`.
* Distinguer deux champs du mÃªme type au sein d'un _Tuple_ : \
  â†’ Exemple :&#x20;

```fsharp
type ComplexNumber =
    | Cartesian of Real: float * Imaginary: float
    | Polar of Magnitude: float * Phase: float
```

â˜ **Notes :**

* Le nommage des champs est optionnel.
* En tant que champ, on optera pour le PascalCase.\
  Mais on peut aussi les voir en tant que paramÃ¨tres, alors en camelCase.
* Quand un _case_ contient plusieurs champs, on peut n'en nommer que certains.\
  â†’ Mais je ne recommande pas cette dissymÃ©trie.

## DÃ©claration

Sur plusieurs lignes : 1 ligne / _case_ â†’ â˜ Ligne indentÃ©e et commenÃ§ant par `|`

Sur une seule ligne -- si dÃ©claration reste **courte** â— â†’ ğŸ’¡ Pas besoin du 1er `|`

```fsharp
open System

type IntOrBool =
    | Int32 of Int32                        // ğŸ’¡ Tag de mÃªme nom que ses donnÃ©es
    | Boolean of Boolean

type OrderId = OrderId of int               // ğŸ‘ˆ Single-case union
                                            // ğŸ’¡ Tag de mÃªme nom que l'union parent
type Found<'T> = Found of 'T | NotFound     // ğŸ’¡ Type gÃ©nÃ©rique
```

## Instanciation

_Tag_ â‰ƒ **constructeur** â†’ Fonction appelÃ©e avec les Ã©ventuelles donnÃ©es du _case_

```fsharp
type Shape =
    | Circle of radius: int
    | Rectangle of width: int * height: int

let circle = Circle 12          // Type: 'Shape', Valeur: 'Circle 12'
let rect = Rectangle (4, 3)     // Type: 'Shape', Valeur: 'Rectangle (4, 3)'

// Utilisation du nom des champs
let rec2 = Rectangle (height = 4, width = 6)

let circles = [1..4] |> List.map Circle     // ğŸ‘ˆ Tag employÃ© comme fonction
```

## Conflit de noms

Quand 2 unions ont des tags de mÃªme nom â†’ Qualifier le tag avec le nom de l'union

```fsharp
type Shape =
    | Circle of radius: int
    | Rectangle of width: int * height: int

type Draw = Line | Circle       // 'Circle' sera en conflit avec le tag de 'Shape'

let draw = Circle              // Type='Draw' (type le + proche) -- âš ï¸ Ã  Ã©viter car ambigu

// Tags qualifiÃ©s par leur type union
let shape = Shape.Circle 12
let draw' = Draw.Circle
```

## AccÃ¨s aux donnÃ©es internes

Uniquement via _pattern matching_ Matching d'un type Union est **exhaustif**

```fsharp
type Shape =
    | Circle of radius: float
    | Rectangle of width: float * height: float

let area shape =
    match shape with
    | Circle r -> Math.PI * r * r   // ğŸ’¡ MÃªme syntaxe que instanciation
    | Rectangle (w, h) -> w * h

let isFlat = function 
    | Circle 0.                     // ğŸ’¡ Constant pattern
    | Rectangle (0., _)
    | Rectangle (_, 0.) -> true     // ğŸ’¡ OR pattern
    | Circle _
    | Rectangle _ -> false
```

## &#x53;_&#x69;ngle-case union_

Union avec un seul cas encapsulant un type (gÃ©nÃ©ralement primitif)

```fsharp
type CustomerId = CustomerId of int
type OrderId = OrderId of int

let fetchOrder (OrderId orderId) =    // ğŸ’¡ DÃ©construction directe sans 'match'
    ...
```

Assure _type safety_ contrairement au simple type alias â†’ Impossible de passer un `CustomerId` Ã  une fonction attendant un `OrderId` ğŸ‘

Permet d'Ã©viter _Primitive Obsession_ Ã  coÃ»t minime

## Style "enum"

Tous les _cases_ sont vides = dÃ©pourvus de donnÃ©es â†’ â‰  `enum` .NET ğŸ“[enums.md](enums.md "mention")

L'instanciation et le pattern matching se font juste avec le _tag_ â†’ Le _tag_ n'est plus une ~~fonction~~ mais une valeur _(_[_singleton_](https://fsharpforfunandprofit.com/posts/fsharp-decompiled/#enum-style-unions)_)_

```fsharp
type Answer = Yes | No | Maybe
let answer = Yes

let print answer =
    match answer with
    | Yes   -> printfn "Oui"
    | No    -> printfn "Non"
    | Maybe -> printfn "Peut-Ãªtre"
```
