# Enums

## D√©claration

Ensemble de constantes nomm√©es dont la valeur est de type entier :\
‚Üí Contrairement au C‚ôØ, il faut d√©finir la valeur de tous les membres de l'enum :&#x20;

```fsharp
type ColorN =
    | Red   = 1
    | Green = 2
    | Blue  = 3
```

‚òùÔ∏è Noter la diff√©rence de syntaxe avec un type union "enum-like" ([#style-enum](unions.md#style-enum "mention")) :&#x20;

```fsharp
type Color = Red | Green | Blue
```

### Type sous-jacent

* Contrairement au C‚ôØ, il n'y a pas de type sous-jacent par d√©faut en F‚ôØ.
* Le type sous-jacent est d√©fini au moyen des litt√©raux d√©finissant les valeurs des membres :&#x20;
  * `1`, `2`, `3` ‚Üí `int`
  * `1uy`, `2uy`, `3uy` ‚Üí `byte`
  * Etc. - cf. [Literals](https://docs.microsoft.com/en-us/dotnet/fsharp/language-reference/literals)

Corolaire : il faut utiliser le m√™me type pour tous les membres, sinon cela ne compile pas !

```fsharp
type ColorN =
    | Red   = 1
    | Green = 2
    | Blue  = 3uy
// üí• ~~~~~~~~~~~
// Cette expression √©tait cens√©e avoir le type `int` 
// mais elle a ici le type `byte`
```

### Char

Le type `char` peut √™tre utilis√© comme type sous-jacent :&#x20;

```fsharp
type AnswerChar = Yes='Y' | No='N'

// L'√©quivalent ne marche pas avec 'string'
type AnswerChar = Yes="Y" | No="N"
// üí• ~~~~~~~~~~ Litt√©raux √©num√©r√©s doivent √™tre de type 'int'...
```

### Casse

Autre diff√©rence avec les types union, les membres peuvent √™tre en camelCase :&#x20;

```fsharp
type File =
    | a = 'a'
    | b = 'b'
    | c = 'c'
```

## Usage

:warning: Contrairement aux unions, l'emploi d'un membre _(a.k.a litt√©ral)_ d'enum est forc√©ment **qualifi√©**

```fsharp
let answerKo = Yes            // üí• Error FS0039
//             ~~~ La valeur ou le constructeur 'Yes' n'est pas d√©fini.
let answer = AnswerChar.Yes   // üëå OK
```

Cast via helpers `int` et `enum` (mais pas `char`) :

```fsharp
let redValue = int ColorN.Red         // enum -> int
let redAgain = enum<ColorN> redValue  // int -> enum via type g√©n√©rique
let red: ColorN = enum redValue       // int -> enum via annotation

// ‚ö†Ô∏è Ne marche pas avec char enum
let ko = char AnswerChar.No   // üí• Error FS0001
let no: AnswerChar = enum 'N' // üí• Error FS0001
```

## Pattern matching

:warning: Contrairement aux unions, le _pattern matching_ **n'est pas exhaustif**

```fsharp
type ColorN = Red=1 | Green=2 | Blue=3

let toHex color =
    match color with
    | ColorN.Red   -> "FF0000"
    | ColorN.Green -> "00FF00"
    | ColorN.Blue  -> "0000FF"
    // ‚ö†Ô∏è Warning FS0104: Les enums peuvent accepter des valeurs en dehors des cas connus.
    // Par exemple, la valeur 'enum<ColorN> (0)' peut indiquer un cas non trait√©...

    // üí° Pour enlever le warning, il faut ajouter un pattern g√©n√©rique
    | _ -> invalidArg (nameof color) $"Color {color} not supported"
```

## Valeurs

On peut utiliser la m√©thode `System.Enum.GetValues()` pour obtenir la liste des membres d'une `enum`. Par contre, le type de retour est faiblement typ√© : `Array` (tableau non g√©n√©rique). \
‚Üí Il suffit d'encapsuler cette m√©thode dans une fonction _helper_ telle que :&#x20;

```fsharp
let enumValues<'a> () =
    Enum.GetValues(typeof<'a>)
    :?> ('a array)
    |> Array.toList

let allPermissions = enumValues<PermissionFlags>()
// val allPermissions: PermissionFlags list = [Read; Write; Execute]
```

üí° Voir √©galement  [#extras](enums.md#extras "mention")

## Flags

M√™me principe qu'en C‚ôØ o√π l'on choisit comme valeurs des multiples de 2 afin de pouvoir les combiner :

```fsharp
open System

[<Flags>]
type PermissionFlags =
    | Read    = 1
    | Write   = 2
    | Execute = 4

let permissions = PermissionFlags.Read ||| PermissionFlags.Write
// val permissions: PermissionFlags = Read, Write

let canRead    = permissions.HasFlag PermissionFlags.Read    // true
let canWrite   = permissions.HasFlag PermissionFlags.Write   // true
let canExecute = permissions.HasFlag PermissionFlags.Execute // false
```

üí° **Notes :**&#x20;

* L'attribut `System.FlagsAttribute` est facultatif mais permet d'avoir un code plus explicite. En outre, il am√©liore le rendu des combinaisons de flags : dans l'exemple pr√©c√©dent, la valeur affich√©e de `permissions` est `Read, Write`. Sans l'attribut `Flags`, cela aurait afficher `3`.
* Op√©rateur OU binaire `|||` _(`|` en C‚ôØ)_ pour combiner des flags

üí° **Astuce :** utiliser la notation binaire pour la valeur des flags : &#x20;

```fsharp
[<Flags>]
type PermissionFlags =
    | Read    = 0b001
    | Write   = 0b010
    | Execute = 0b100
```

### Combinaisons

En C‚ôØ, l'on peut directement combiner des flags, comme `ReadWrite` et `All` ci-dessous :&#x20;

```csharp
[Flags]
public enum PermissionFlags 
{
    Read    = 0b001,
    Write   = 0b010,
    Execute = 0b100,

    ReadWrite = Read | Write,
    All = Read | Write | Execute
}
```

Ce n'est pas possible en F‚ôØ mais 2 alternatives sont possibles :&#x20;

1\) Proc√©der manuellement aux combinaisons binaires : ce n'est pas dur √† faire mais cela nuit √† la visibilit√© car cela demande un peu de r√©flexion pour retrouver les flags initiaux. On notera aussi que ces combinaisons font partie int√©grante de l'enum.

```fsharp
[<Flags>]
type PermissionFlags =
    | Read      = 0b001
    | Write     = 0b010
    | Execute   = 0b100
    | ReadWrite = 0b011 // ‚ùå Moins explicite que Read | Write
    | All       = 0b111 // ‚ùå Moins explicite que Read | Write | Execute 

let all = PermissionFlags.All
// val all: PermissionFlags = All

let all' = PermissionFlags.Read ||| PermissionFlags.Write
// val all': PermissionFlags = ReadWrite
// ‚úîÔ∏è Recombinaison automatique du compilateur reconnaissant "All"
```

2\) Utiliser un module compagnon : pour rendre les combinaisons explicites dans le code mais non reconnues / recombin√©es par le compilateur.

```fsharp
[<System.Flags>]
type Spacing =
    | Left   = 0b0001
    | Right  = 0b0010
    | Top    = 0b0100
    | Bottom = 0b1000

[<RequireQualifiedAccess>]
module Spacing =
    let Horizontal = Spacing.Left ||| Spacing.Right  // ‚úîÔ∏è Human-friendly
    let Vertical = Spacing.Top ||| Spacing.Bottom
    let All = Horizontal ||| Vertical

let horizontal = Spacing.Horizontal
// val horizontal: Spacing = Left, Right
// ‚ùå Not "Horizontal"
```

‚òù **Note :** la m√©thode `HasFlag` a un comportement diff√©rent selon l'option utilis√©e. C'est mis en valeur dans l'exemple ci-dessous d√©finissant les 2 helpers `Enum.values` d√©j√† vu plus haut et `Enum.flags` qui d√©compose une valeur d'enum en ses flags √©l√©mentaires.

```fsharp
[<RequireQualifiedAccess>]
module Enum =
    let values<'enum when 'enum :> System.Enum> =
        System.Enum.GetValues(typeof<'enum>)
        :?> 'enum array
        |> Array.toList

    let flags<'enum when 'enum :> System.Enum> (enumValue: 'enum) =
        values<'enum>
        |> List.filter (enumValue.HasFlag)

let flagsInline = Enum.flags PermissionFlags.All
// val flagsInline: PermissionFlags list = [Read; Write; ReadWrite; Execute; All]
// üëâ Includes "ReadWrite" and "All". Could even include a "None = 0" ‚ùó

let flagsCompanion = Enum.flags Spacing.All
// val flagsCompanion: Spacing list = [Left; Right; Top; Bottom]
// üëç Only includes core flags
```

## Enum _vs_ Union

| Type               | Enum             | Union                |
| ------------------ | ---------------- | -------------------- |
| Type sous-jacent   | Enti√®res ou char | Quelconques          |
| Qualification      | Obligatoire      | Qu'en cas de conflit |
| Matching exhaustif | ‚ùå Non            | ‚úÖ Oui                |
| PascalCase         | ‚úÖ Oui            | ‚úÖ Oui                |
| camelCase          | ‚úÖ Oui            | ‚ùå Non                |

‚òù **Recommandation :**

* Pr√©f√©rer une Union dans la majorit√© des cas
* Choisir une Enum pour :
  * Interop .NET
  * Besoin de lier des donn√©es de type `int`

## Conversion

`enum<'enum>` : permet de convertir une valeur enti√®re en l'enum `'enum` sp√©cifi√©e

```fsharp
type AnswerNum =
    | Yes = 1
    | No  = 0

let y1 = enum<AnswerNum> 1  // val y1 : AnswerNum = Yes
let y2: AnswerNum = enum 0  // val y2 : AnswerNum = No
```

`int` permet la conversion inverse pour r√©cup√©rer la valeur sous-jacente d'un membre d'une enum

```fsharp
let yesNum = int AnswerNum.Yes  // val yesNum : int = 1
```

### Char enum

Pour les enums dont le type sous-jacent est `char`, les fonctions `enum`, `int` et `char` ne marchent pas :&#x20;

```fsharp
type AnswerChar =
    | Yes = 'Y'
    | No  = 'N'

let no_ko: AnswerChar = enum 'N' // üí• Le type 'int32' ne correspond pas au type 'char'

let y1_ko = int AnswerChar.Yes   // üí• Le type 'AnswerChar' ne prend pas en charge une conversion vers le type 'int'
let y2_ko = char AnswerChar.Yes  // üí• Le type 'AnswerChar' ne prend pas en charge une conversion vers le type 'char'
```

Il faut alors utiliser le module `LanguagePrimitives` :&#x20;

```fsharp
let no_ok: AnswerChar = LanguagePrimitives.EnumOfValue 'N' // val no_ok : AnswerChar = No

let y1_ok = LanguagePrimitives.EnumToValue AnswerChar.Yes // val y1_ok : char = 'Y'
let y2_ok = unbox<char> AnswerChar.Yes  // val y2_ok : char = 'Y'
```

## Extras

Le package NuGet [FSharpx.Extras](https://github.com/fsprojects/FSharpx.Extras) comporte un module Enum proposant ces helpers :&#x20;

* `parse<'enum>: string -> 'enum`
* `tryParse<'enum>: string -> 'enum option`
* `getValues<'enum>: unit -> 'enum seq`

```fsharp
#r "nuget: FSharpx.Extras"
open FSharpx

type ColorN =
    | Red   = 1
    | Green = 2
    | Blue  = 3

let red = "Red" |> Enum.tryParse<ColorN> // val red: ColorN option = Some Red
let none = "xx" |> Enum.tryParse<ColorN> // val none: ColorN option = None

let blue: ColorN = "Blue" |> Enum.parse // val blue: ColorN = Blue
let ko =
    try
        "Ko" |> Enum.parse<ColorN>
    with ex ->
        printfn "üí• %s %s" (ex.GetType().FullName) ex.Message
        // üí• System.ArgumentException: Requested value 'Ko' was not found
        enum<ColorN> 0

let colors = Enum.getValues<ColorN> () |> Seq.toList
// val colors: ColorN list = [Red; Green; Blue]
```
