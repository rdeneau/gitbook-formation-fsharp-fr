# Fonctions

## Binding d'une fonction

`let f x y = x + y + 1`

* Binding r√©alis√© avec mot cl√© `let`
* Associe √† la fois un nom (`f`) et les param√®tres (`x` et `y`)
* Annotation de type optionnelle pour param√®tres et/ou retour
  * `let f (x: int) (y: int) : int = ...`
  * Sinon, inf√©rence de type, avec possible g√©n√©ralisation auto
* Derni√®re expression ‚Üí valeur de retour de la fonction
* Possible d√©finition de sous-fonctions (non g√©n√©riques)

## Fonction g√©n√©rique

* Dans beaucoup de cas, inf√©rence marche avec g√©n√©ralisation auto
  * `let listOf x = [x]` ‚Üí `(x: 'a) -> 'a list`
* Annotation explicite de params g√©n√©riques
  * `let f (x: 'a) = ...` (pas besoin de faire `f<'a>` gr√¢ce au `'` üëç)
* Annotation explicite avec inf√©rence du type g√©n√©rique
  * `let f (list: list<_>) = ...`

## Fonction anonyme / Lambda

Expression d√©finissant une fonction

Syntaxe : `fun parameter1 parameter2 etc -> expression`

‚òù **√Ä noter :**

* Mot cl√© `fun` obligatoire
* Fl√®che fine `->` (Java) ‚â† fl√®che grasse `=>` (C‚ôØ, Js)

### Lambda : quelques usages

#### **1.** En argument d'une _high-order function_

* Pour √©viter de devoir d√©finir une fonction nomm√©e
* Recommand√©e pour une fonction courte, pour que cela reste lisible

```fsharp
[1..10] |> List.map (fun i -> i + 1) // üëà () autour de la lambda

// Versus en passant par une fonction nomm√©e
let add1 i = i + 1
[1..10] |> List.map add1
```

:warning: Attention au lambda inutile : `List.map (fun x -> f x)` ‚â° `List.map f`

#### **2.** Dans un _let binding_ avec inf√©rence

* Pour rendre explicite quand la fonction renvoie une fonction
* Sorte de curryfication manuelle
* √Ä utiliser avec parcimonie :exclamation:

```fsharp
let add x y = x + y                     // Version normale, curryfi√©e automatiquement
let add' x = fun y -> x + y             // Idem avec une sous lambda
let add'' = fun x -> (fun y -> x + y)   // Idem en totalement "lambda-is√©e"
```

#### **3.** Dans un _let binding_ annot√©

* Signature de la fonction pr√©-d√©finie sous forme d'un type
* Type "fonction" s'utilise un peu comme une `interface` C‚ôØ
  * Pour contraindre impl√©mentation √† suivre signature
  * Ex : _Domain modelling made functional_ par Scott Wlaschin

```fsharp
type Add = int -> int -> int

let add: Add =
    fun x y ->
        x + y

// val add: x: int -> y: int -> int
// ‚òù Les param√®tres sont nomm√©s üëç
```

### Conversion en Func

On peut convertir une lambda en `Func<>` mais de mani√®re explicite en passant par son constructeur :&#x20;

```fsharp
let isNull = System.Func<_, _> (fun x -> x = null)
// val isNull: System.Func<'a,bool> when 'a: equality and 'a: null
```

### Conversion en Expression

C'est un peu plus complexe d'√©crire une `Expression<Func<>>` √† partir d'une lambda. Il faut passer par une _quotation_ (\*) puis la convertir en expression en utilisant soit le `FSharp.Linq.RuntimeHelpers.LeafExpressionConverter` pour les cas simples (cf. exemple ci-dessous), soit le package NuGet `FSharp.Quotations.Evaluator` pour les cas plus complexes.

```fsharp
open System
open System.Linq.Expressions
open Microsoft.FSharp.Linq.RuntimeHelpers

let isNullQuot = <@ Func<_, _> (fun (x: obj) -> x = null) @>
// val isNullQuot: Quotations.Expr<System.Func<obj,bool>> =
//   NewDelegate (Func`2, x, Call (None, op_Equality, [x, Value (<null>)]))

let isNullExpr =
    isNullQuot
    |> LeafExpressionConverter.QuotationToExpression 
    |> unbox<Expression<Func<obj, bool>>>
// val isNullExpr: System.Linq.Expressions.Expression<System.Func<obj,bool>> =
//   x => (x == null)
```

**(\*) Note :** Les _quotations_ sont hors scope. Juste 2 mots √† leur propos :\
‚Ä¢ Elles s'√©crivent entre `<@ ... @>`.\
‚Ä¢ Ce sont un peu l'√©quivalent des Expressions en C# - plus d'info [ici](https://stackoverflow.com/a/8134113/8634147).

### Mot cl√© `function`

* Permet aussi de d√©finir une fonction anonyme d√©finissant une expression de match
* Syntaxe abr√©g√©e √©quivalente √† `fun x -> match x with`
* Prend 1 param√®tre qui est implicite

```fsharp
let ouiNon x =
  match x with
  | true  -> "Oui"
  | false -> "Non"
// val ouiNon: x: bool -> string

// üëâ R√©√©crit avec `function`:
let ouiNon = function
  | true  -> "Oui"
  | false -> "Non"
// val ouiNon: _arg1: bool -> string
```

‚òù Son emploi est une question de go√ªt : \
‚Ä¢ Pour les adeptes du point-free - cf. [#style-point-free](operateurs.md#style-point-free "mention") üìç\
‚Ä¢ Plus g√©n√©ralement dans une succession de _pipes_ `arg |> f1 |> function ... |> f2...`\
‚Üí L'id√©e est de faire un pattern matching sur une large expression sans pour autant la stocker dans une variable interm√©diaire.

## D√©construction de param√®tres

* Comme en JavaScript, on peut d√©construire _inline_ un param√®tre
* C'est √©galement une fa√ßon d'indiquer le type du param√®tre
* Le param√®tre appara√Æt sans nom dans la signature

Exemple avec un type _Record_ üìç[records.md](../types-composites/records.md "mention")

```fsharp
type Person = { Name: string; Age: int }

let name { Name = x } = x     // Person -> string
let age { Age = x } = x       // Person -> int
let age' person = person.Age  // Equivalent explicite

let bob = { Name = "Bob"; Age = 18 } // Person
let bobAge = age bob // int = 18
```

On parle aussi de _pattern matching_ \
‚Üí Mais je pr√©f√®re r√©server ce terme pour l'usage de `match x with ...`

D√©construction pas adapt√©e pour un type union avec plusieurs cas üìç [unions.md](../types-composites/unions.md "mention")

* üí° **Exemple :** liste F‚ôØ _(soit vide `[]`, soit valeur + sous-liste `head::tail`)_&#x20;
* :point\_up: **Solution :** faire un _pattern matching_ de tous les cas de l'union

```fsharp
let printFirstItem (x::_) = // 'a list -> unit
//                  ~~~~  Warning FS0025: Crit√®res sp√©ciaux incomplets dans cette expression.
   printfn $"first element: {x}"

let printFirstItemOk = function
    | x::_ -> printfn $"first element: {x}"
    | []   -> printfn "none"
```

## Param√®tre tuple

* Comme en C‚ôØ, on peut vouloir regrouper des param√®tres d'une fonction
  * Par soucis de coh√©sion, quand ces param√®tres forment un tout
  * Pour √©viter le _code smell_ [long parameter list](https://refactoring.guru/smells/long-parameter-list)
* On peut les regrouper dans un tuple et m√™me le d√©construire

```fsharp
// V1 : trop de param√®tres
let f x y z = ...

// V2 : param√®tres regroup√©s dans un tuple
let f params =
    let (x, y, z) = params
    ...

// V3 : idem avec tuple d√©construit sur place
let f (x, y, z) = ...
```

* `f (x, y, z)` ressemble furieusement √† une m√©thode C‚ôØ !
* La signature signale le changement : `(int * int * int) -> TResult`
  * La fonction n'a effectivement plus qu'1! param√®tre plut√¥t que 3
  * Perte possibilit√© application partielle de chaque √©l√©ment du tuple

‚òù **Conclusion** :

* Resister √† la tentation d'utiliser tout le temps un tuple _(car familier - C‚ôØ)_
* R√©server cet usage quand c'est pertinent de regrouper les param√®tres
  * Sans pour autant d√©clarer un type sp√©cifique pour ce groupe

## Fonction r√©cursive

* Fonction qui s'appelle elle-m√™me
* Syntaxe sp√©ciale avec mot cl√© `rec` sinon erreur `FS0039: ‚Ä¶ is not defined`
* Tr√®s courant en F‚ôØ pour remplacer les boucles `for`
  * Car c'est souvent + facile √† concevoir

Exemple : trouver nb √©tapes pour atteindre 1 dans la [suite de Syracuse](https://fr.wikipedia.org/wiki/Conjecture_de_Syracuse) / Collatz

```fsharp
let rec steps (n: int) : int =
    if n = 1       then 0
    elif n % 2 = 0 then 1 + steps (n / 2)
    else                1 + steps (3 * n + 1)
```

## _Tail recursion_

* Type de r√©cursivit√© o√π l'appel r√©cursif est la derni√®re instruction
* D√©tect√© par le compilateur et optimis√© sous forme de boucle
  * Permet d'√©viter les `StackOverflow`
* Proc√©d√© classique pouvant rendre tail r√©cursif :
  * Ajouter un param "accumulateur", comme `fold`/`reduce`

```fsharp
let steps (number: int) : int =
    let rec loop count n = // üëà `loop` = nom idiomatique de ce type de fonction interne r√©cursive
        if n = 1       then count
        elif n % 2 = 0 then loop (count + 1) (n / 2)      // üëà Dernier appel : `loop`
        else                loop (count + 1) (3 * n + 1)  // üëà idem
    loop 0 number // üëà Lancement de la boucle avec 0 comme valeur initiale pour `count`
```

## Fonctions mutuellement r√©cursives

* Fonctions qui s'appellent l'une l'autre
* Doivent √™tre d√©clar√©es ensemble :
  * 1√®re fonction indiqu√©e comme r√©cursive avec `rec`
  * autres fonctions ajout√©es √† la d√©claration avec `and`

```fsharp
// ‚ö†Ô∏è Algo un peu alambiqu√© servant juste d'illustration

let rec Even x =        // üëà Mot cl√© `rec`
    if x = 0 then true
    else Odd (x-1)      // üëà Appel √† `Odd` d√©finie + bas
and Odd x =             // üëà Mot cl√© `and`
    if x = 0 then false
    else Even (x-1)     // üëà Appel √† `Even` d√©finie + haut
```

## Surcharge / _overload_ de fonctions

:warning: Pas possible de surcharger une fonction

üí° Noms diff√©rents :

* `List.map (mapping: 'T -> 'U) list`
* `List.mapi (mapping: (index: int) -> 'T -> 'U) list`

üí° Impl√©mentation via fonction template üëá

## Fonction template

Permet de cr√©er des "surcharges" sp√©cialis√©es :

```fsharp
type ComparisonResult = Bigger | Smaller | Equal

// Fonction template, 'private' pour la "cacher"
let private compareTwoStrings (comparison: StringComparison) string1 string2 =
    let result = System.String.Compare(string1, string2, comparison)
    if result > 0 then
        Bigger
    else if result < 0 then
        Smaller
    else
        Equal

// Application partielle du param√®tre 'comparison'
let compareCaseSensitive   = compareTwoStrings StringComparison.CurrentCulture
let compareCaseInsensitive = compareTwoStrings StringComparison.CurrentCultureIgnoreCase
```

‚òù Emplacement du param√®tre de sp√©cialisation :

* En C‚ôØ, en dernier :

```csharp
String.Compare(String, String, StringComparison)
String.Compare(String, String)
```

* En F‚ôØ, en premier pour permettre application partielle :

```fsharp
compareTwoStrings    : StringComparison -> String -> String -> ComparisonResult
compareCaseSensitive :                     String -> String -> ComparisonResult
```

## Fonction `inline`

### Principe

> **Extension inline** ou **inlining :** optimisation d'un compilateur qui remplace un appel de fonction par le code _(le corps)_ de cette fonction.\
> üîó [https://fr.wikipedia.org/wiki/Extension\_inline](https://fr.wikipedia.org/wiki/Extension_inline)

* Gain de performance üëç
* Compilation + longue :warning:

üí° Similaire au refacto [_Inline Method_](https://refactoring.guru/inline-method)

### Mot cl√© `inline`

Indique au compilateur de _"inliner"_ la fonction

Usage typique : petite fonction/op√©rateur de "sucre syntaxique"

Exemples issus de [FSharp.Core](https://github.com/dotnet/fsharp/blob/main/src/fsharp/FSharp.Core/prim-types.fs)¬†: \
‚Üí Fonction `ignore`    üìç [fonctions-standard.md](fonctions-standard.md "mention")\
‚Üí Op√©rateur `|>`          üìç [#operateur-pipe-right-or-greater-than](operateurs.md#operateur-pipe-right-or-greater-than "mention")

```fsharp
let inline ignore _ = ()
let inline (|>) x f = f x

let t = true |> ignore
//   ~= ignore true // Apr√®s inline du pipe
//   ~= ()          // Apr√®s inline de ignore
```

L'autre usage de fonctions `inline` concerne le [#srtp](../types-complements/generiques.md#srtp "mention") üìç

