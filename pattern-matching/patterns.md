# Patterns

## GÃ©nÃ©ralitÃ©s

_Patterns_ = rÃ¨gles pour dÃ©tecter la structure de donnÃ©es en entrÃ©e

UtilisÃ©s abondamment en Fâ™¯

* Dans _match expression_, _let binding_ de valeurs et de paramÃ¨tres de fonctions
* TrÃ¨s pratiques pour manipuler les types algÃ©briques Fâ™¯ (tuple, record, union)
* Composables : supporte plusieurs niveaux d'imbrication
* Assemblables par ET/OU logiques
* Supporte les littÃ©raux : `1.0`, `"test"`...

## Wildcard Pattern

ReprÃ©sentÃ© par `_`, seul ou combinÃ© avec un autre _pattern_

Toujours vrai â†’ A placer en dernier dans une _match expression_

:warning: Toujours chercher en 1er Ã  traiter exhaustivement/explicitement tous les cas\
&#x20;     Quand impossible, utiliser alors le `_`

```fsharp
match option with
| Some 1 -> ...
| _ -> ...              // âš ï¸ Non exhaustif

match option with
| Some 1 -> ...
| Some _ | None -> ...  // ğŸ‘Œ \+ exhaustif
```

## Constant Pattern

DÃ©tecte constantes, `null` et littÃ©raux de nombre, `char`, `string`, `enum`

```fsharp
[<Literal>]
let Three = 3   // Constante

let is123 num = // int -> bool
    match num with
    | 1 | 2 | Three -> true
    | _ -> false
```

â˜ **Notes :**

* Le pattern de `Three` est aussi classÃ© en tant que _Identifier Pattern_ ğŸ“
* Pour le matching de `null`, on parle aussi de _Null Pattern_

## Identifier Pattern

DÃ©tecte les _cases_ d'un type union ainsi que leur Ã©ventuel contenu

```fsharp
type PersonName =
    | FirstOnly of string
    | LastOnly  of string
    | FirstLast of string * string

let classify personName =
    match personName with
    | FirstOnly _ -> "First name only"
    | LastOnly  _ -> "Last name only"
    | FirstLast _ -> "First and last names"
```

## Variable Pattern

Assigne la valeur dÃ©tectÃ©e Ã  une "variable" pour l'utiliser ensuite

Exemple : variables `firstName` et `lastName` ci-dessous

```fsharp
type PersonName =
    | FirstOnly of string
    | LastOnly  of string
    | FirstLast of string * string

let confirm personName =
    match personName with
    | FirstOnly (firstName) -> printf "May I call you %s?" firstName
    | LastOnly  (lastName) -> printf "Are you Mr. or Ms. %s?" lastName
    | FirstLast (firstName, lastName) -> printf "Are you %s %s?" firstName lastName
```

:warning: On ne peut pas lier Ã  plusieurs reprises vers la mÃªme variable

```fsharp
let elementsAreEqualKo tuple =
    match tuple with
    | (x,x) -> true  // ğŸ’¥ Error FS0038: 'x' est liÃ© Ã  deux reprises dans ce modÃ¨le
    | (_,_) -> false
```

**Solutions :** utiliser 2 variables puis vÃ©rifier l'Ã©galitÃ©

```fsharp
// 1. Guard clauseğŸ“
let elementsAreEqualOk = function
    | (x,y) when x = y -> true
    | (_,_) -> false

// 2. DÃ©construction
let elementsAreEqualOk' (x, y) = x = y
```

### Champs nommÃ©s de _cases_ d'union

Plusieurs possibilitÃ©s :

1. Pattern "anonyme" du tuple complet\
   â†’ Il faut dÃ©construire tous les champs du tuple.\
   â†’ On utilise la virgule `,` pour sÃ©parer ces champs.
2. Pattern d'un seul champ par son nom â†’ `Field = value`
3. Pattern de plusieurs champs par leur nom â†’ `F1 = v1; F2 = v2`\
   :warning: Ne pas confondre avec l'option 1 : \
   â†’ Le dÃ©limiteur est ici le **point-virgule** `;` et non la virgule `,` !

```fsharp
type Shape =
    | Rectangle of Height: int * Width: int
    | Circle of Radius: int

let describe shape =
    match shape with
    | Rectangle (0, _)                                              // (1)
    | Rectangle (Height = 0)            -> "Flat rectangle"         // (2)
    | Rectangle (Width = w; Height = h) -> $"Rectangle {w} Ã— {h}"   // (3)
    | Circle radius                     -> $"Circle âˆ… {2*radius}"
```

## Alias Pattern

`as` permet de nommer un Ã©lÃ©ment dont le contenu est dÃ©construit

```fsharp
let (x, y) as coordinate = (1, 2)
printfn "%i %i %A" x y coordinate  // 1 2 (1, 2)
```

ğŸ’¡ Marche aussi dans les fonctions :

```fsharp
type Person = { Name: string; Age: int }

let acceptMajorOnly ({ Age = age } as person) =
    if age < 18 then None else Some person
```

## OR et AND Patterns

Permettent de combiner deux patterns _(nommÃ©s `P1` et `P2` ci-aprÃ¨s)_

* `P1 | P2` â†’ P1 ou P2. Ex : `Rectangle (0, _) | Rectangle (_, 0)`
* `P1 & P2` â†’ P1 et P2. UtilisÃ© surtout avec [active-patterns.md](active-patterns.md "mention")ğŸ“

ğŸ’¡ On peut utiliser la mÃªme variable _(`name` ci-dessous)_ dans 2 patterns :

```fsharp
type Upload = { Filename: string; Title: string option }

let titleOrFile ({ Title = Some name } | { Filename = name }) = name

titleOrFile { Filename = "Report.docx"; Title = None }            // Report.docx
titleOrFile { Filename = "Report.docx"; Title = Some "Report+" }  // "Report+"
```

## Parenthesized Pattern

Usage des parenthÃ¨ses `()` pour grouper des patterns, pour gÃ©rer la prÃ©cÃ©dence

```fsharp
type Shape = Circle of Radius: int | Square of Side: int

let countFlatShapes shapes =
    let rec loop rest count =
        match rest with
        | (Square (Side = 0) | (Circle (Radius = 0))) :: tail -> loop tail (count + 1) // â‘ 
        | _ :: tail -> loop tail count
        | [] -> count
    loop shapes 0
```

â˜ **Note :** la ligne â‘  ne compilerait sans faire `() :: tail`

:warning: Les parenthÃ¨ses compliquent la lecture&#x20;

ğŸ’¡ Essayer de s'en passer quand c'est possible

```fsharp
let countFlatShapes shapes =
    let rec loop rest count =
        match rest with
        | Circle (Radius = 0) :: tail
        | Square (Side = 0) :: tail
          -> loop tail (count + 1)
        // [...]
```

## Construction Patterns

Reprennent syntaxe de construction d'un type pour le dÃ©construire

* Cons et List Patterns
* Array Pattern
* Tuple Pattern
* Record Pattern

### Cons et List Patterns

â‰ƒ Inverses de 2 types de construction d'une liste, avec mÃªme syntaxe

_Cons Pattern_ : `head :: tail` â†’ dÃ©compose une liste _(avec >= 1 Ã©lÃ©ment)_ en :

* _Head_ : 1er Ã©lÃ©ment
* _Tail_ : autre liste avec le reste des Ã©lÃ©ments - peut Ãªtre vide

_List Pattern_ : `[items]` â†’ dÃ©compose une liste en 0..N Ã©lÃ©ments

* `[]` : liste vide
* `[x]` : liste avec 1 Ã©lÃ©ment mis dans la variable `x`
* `[x; y]` : liste avec 2 Ã©lÃ©ments mis dans les variables `x` et `y`
* `[_; _]` : liste avec 2 Ã©lÃ©ments ignorÃ©s

ğŸ’¡ `x :: []` â‰¡ `[x]`, `x :: y :: []` â‰¡ `[x; y]`...

La _match expression_ par dÃ©faut combine les 2 patterns : â†’ Une liste est soit vide `[]`, soit composÃ©e d'un item et du reste : `head :: tail`

Les fonctions rÃ©cursives parcourant une liste utilise le pattern `[]` pour stopper la rÃ©cursion :

```fsharp
let rec printList l =
    match l with
    | head :: tail ->
        printf "%d " head
        printList tail     // RÃ©cursion sur le reste
    | [] -> printfn ""     // Fin de rÃ©cursion : liste parcourue entiÃ¨rement
```

### Array Pattern

Syntaxe: `[| items |]` pour 0..N items entre `;`

```fsharp
let length vector =
    match vector with
    | [| x |] -> x
    | [| x; y |] -> sqrt (x*x + y*y)
    | [| x; y; z |] -> sqrt (x*x + y*y + z*z)
    | _ -> invalidArg (nameof vector) $"Vector with more than 4 dimensions not supported"
```

â˜ Il n'existe pas de pattern pour les sÃ©quences, vu qu'elles sont _"lazy"_.

### Tuple Pattern

Syntaxe : `items` ou `(items)` pour 2..N items entre `,`

ğŸ’¡ Pratique pour pattern matcher plusieurs valeurs en mÃªme temps

```fsharp
type Color = Red | Blue
type Style = Background | Text

let css color style =
    match color, style with
    | Red, Background -> "background-color: red"
    | Red, Text -> "color: red"
    | Blue, Background -> "background-color: blue"
    | Blue, Text -> "color: blue"
```

### Record Pattern

Syntaxe : `{ Fields }` pour 1..N `Field = variable` entre `;`

* Pas obligÃ© de spÃ©cifier tous les champs du Record
* En cas d'ambiguÃ¯tÃ©, qualifier le champ : `Record.Field`

ğŸ’¡ Marche aussi pour les paramÃ¨tres d'une fonction :

```fsharp
type Person = { Name: string; Age: int }

let displayMajority { Age = age; Name = name } =
    if age >= 18
    then printfn "%s is major" name
    else printfn "%s is minor" name

let john = { Name = "John"; Age = 25 }
displayMajority john // John is major
```

:warning: **Rappel :** il n'y a pas de pattern pour les _Records_ anonymes !

```fsharp
type Person = { Name: string; Age: int }

let john = { Name = "John"; Age = 25 }
let { Name = name } = john  // ğŸ‘Œ val name : string = "John"

let john' = {| john with Civility = "Mister" |}
let {| Name = name' |} = john'  // ğŸ’¥
```

## Type Test Pattern

**Syntaxe :** `my-object :? sub-type`&#x20;

Renvoie un `bool` \
â†’ `is` Câ™¯ (`my-object is sub-type`)

**Usage :** avec une hiÃ©rarchie de types

```fsharp
open System.Windows.Forms

let RegisterControl (control: Control) =
    match control with
    | :? Button as button -> button.Text <- "Registered."
    | :? CheckBox as checkbox -> checkbox.Text <- "Registered."
    | :? Windows -> invalidArg (nameof control) "Window cannot be registered"
    | _ -> ()
```

:point\_up: **Remarque :** l'opÃ©rateur `:?` n'est pas conÃ§u pour vÃ©rifier des Ã©vidences car il adopte alors un comportement dÃ©routant : \
&#xNAN;_(Exemple tirÃ© de cette_ [_question stackoverflow_](https://stackoverflow.com/q/70845434/8634147)_)_

```fsharp
type Car = interface end

type Mercedes() =
    interface Car

let merc = Mercedes ()

merc :? Mercedes        // true with warning FS0067: Ce test de type ... conviendra toujours.
box merc :? Mercedes    // true
merc :? Car             // error FS0193: IncompatibilitÃ© de contrainte de type.
box merc :? Car         // true
```

### Bloc `try`/`with`

On rencontre frÃ©quemment ce pattern dans les blocs `try`/`with` :

```fsharp
try
    printfn "Difference: %i" (42 / 0)
with
| :? DivideByZeroException as x -> 
    printfn "Fail! %s" x.Message
| :? TimeoutException -> 
    printfn "Fail! Took too long"
```

### Boxing

Le _Type Test Pattern_ ne marche qu'avec des types rÃ©fÃ©rences.&#x20;

â†’ Pour un type valeur ou inconnu, il faut le convertir en objet _(a.k.a boxing)_

```fsharp
let isIntKo = function :? int -> true | _ -> false
// ğŸ’¥ Error FS0008: test de type au moment de l'exÃ©cution du type 'a en int...

let isInt x =
    match box x with
    | :? int -> true
    | _ -> false
```
