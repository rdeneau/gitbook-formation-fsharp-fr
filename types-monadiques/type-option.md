# Type Option

## PrÃ©sentation

A.k.a `Maybe` _(Haskell),_ `Optional` _(Java 8)_

ModÃ©lise l'absence de valeur \
â†’ Dans le sens d'une possibilitÃ© pour une valeur d'Ãªtre absente\
â†’ DiffÃ©rent de `unit` qui est utilisÃ© dans le cas oÃ¹ il n'y a jamais de valeur

DÃ©fini sous la forme d'une union avec 2 _cases_ :

```fsharp
type Option<'Value> =
    | None              // Case sans donnÃ©e â†’ quand valeur absente
    | Some of 'Value    // Case avec donnÃ©e â†’ quand valeur prÃ©sente
```

## Cas d'utilisation

### ModÃ©liser un champ optionnel

```fsharp
type Civility = Mr | Mrs
type User = { Name: string; Civility: Civility option }

let joey  = { Name = "Joey"; Civility = Some Mr }
let guest = { Name = "Guest"; Civility = None }
```

â†’ Rend explicite le fait que `Name` est obligatoire et `Civility` facultatif

â˜ **Attention :** ce design n'empÃªche pas ici d'avoir `Name = null` _(limite BCL)_

### OpÃ©ration partielle

OpÃ©ration oÃ¹ aucune valeur de sortie n'est possible pour certaines entrÃ©es.

## Exemples

### Exemple 1 - Inverse d'un nombre

```fsharp
let inverse n = 1.0 / n

let tryInverse n =
    match n with
    | 0.0 -> None
    | n   -> Some (1.0 / n)
```

| **Fonction** | **OpÃ©ration** | **Signature**           | `n = 0.5`  | `n = 0.0`    |
| ------------ | ------------- | ----------------------- | ---------- | ------------ |
| `inverse`    | Partielle     | `float -> float`        | `2.0`      | `infinity` â“ |
| `tryInverse` | Totale        | `float -> float option` | `Some 2.0` | `None` ğŸ‘Œ    |

### Exemple 2 - Recherche d'un Ã©lÃ©ment dans une collection

* OpÃ©ration partielle : `find predicate` â†’ ğŸ’¥ quand Ã©lÃ©ment non trouvÃ©
* OpÃ©ration totale : `tryFind predicate` â†’ `None` ou `Some item`

## Avantages ğŸ‘

* Explicite (honnÃªte) concernant la partialitÃ© de l'opÃ©ration
  * Pas de valeur spÃ©ciale (et cachÃ©e) : `null`, `infinity`
  * Pas d'exception
* Force le code appelant Ã  gÃ©rer la totalitÃ© des cas :
  * PrÃ©sence d'une valeur en sortie : `Some value`
  * Absence d'une valeur en sortie : `None`

## Flux de contrÃ´le

Pour tester la prÃ©sence de la valeur _(de type `'T`)_ dans l'option

* âŒ Ne pas utiliser `IsSome`, `IsNone` et `Value` (ğŸ¤ğŸ’¥)
  * ~~if option.IsSome then option.Value...~~
* ğŸ‘Œ A la main avec _pattern matching_
* âœ… Fonctions du module `Option`

### Manuel avec _pattern matching_

Exemple :

```fsharp
let print option =
    match option with
    | Some x -> printfn "%A" x
    | None   -> printfn "None"

print (Some 1.0)  // 1.0
print None        // None
```

### IntÃ©grÃ© au module `Option`

OpÃ©ration de _Mapping_ de la valeur (de type `'T`) **si âˆƒ** :

* `option |> Option.map  f` avec `f` opÃ©ration totale `'T -> 'U`
* `option |> Option.bind f` avec `f` opÃ©ration partielle `'T -> 'U option`

Conserver la valeur **si âˆƒ** et si respecte condition :

* `option |> Option.filter predicate` avec `predicate: 'T -> bool` appelÃ© que si valeur âˆƒ

### Exercice

ImplÃ©menter `map`, `bind` et `filter` avec _pattern matching_

### Solution

```fsharp
let map f option =             // (f: 'T -> 'U) -> 'T option -> 'U option
    match option with
    | Some x -> Some (f x)
    | None   -> None           // ğŸ 1. Pourquoi on ne peut pas Ã©crire `None -> option` ?

let bind f option =            // (f: 'T -> 'U option) -> 'T option -> 'U option
    match option with
    | Some x -> f x
    | None   -> None

let filter predicate option =  // (predicate: 'T -> bool) -> 'T option -> 'T option
    match option with
    | Some x when predicate x -> option
    | _ -> None                // ğŸ 2. ImplÃ©menter `filter` avec `bind` ?
```

### RÃ©ponses ğŸ

```fsharp
// ğŸ 1. Pourquoi on ne peut pas Ã©crire `None -> option` :
let map (f: 'T -> 'U) (option: 'T option) : 'U option =
    match option with
    | Some x -> Some (f x)
    | None   -> (*None*) option  // ğŸ’¥ Erreur de typage : `'U option` attendu != `'T option`
```

```fsharp
// ğŸ 2. ImplÃ©menter `filter` avec `bind` :
let filter predicate option =  // (predicate: 'T -> bool) -> 'T option -> 'T option
    let f x = if predicate x then option else None
    bind f option
```

### Exemple

```fsharp
// Application console de questions/rÃ©ponses
type Answer = A | B | C | D

let tryParseAnswer text =
    match text with
    | "A" -> Some A
    | "B" -> Some B
    | "C" -> Some C
    | "D" -> Some D
    | _   -> None

// Fonction appelÃ©e quand l'utilisateur saisit la rÃ©ponse au clavier Ã  la question posÃ©e
let checkAnswer (expectedAnswer: Answer) (givenAnswer: string) =
    tryParseAnswer givenAnswer
    |> Option.filter ((=) expectedAnswer)
    |> Option.map (fun _ -> "âœ…")
    |> Option.defaultValue "âŒ"

["X"; "A"; "B"] |> List.map (checkAnswer B)  // ["âŒ"; "âŒ"; "âœ…"]
```

### BÃ©nÃ©fices

Rend logique mÃ©tier + lisible

* Pas de `if hasValue then / else`
* Met en valeur le _happy path_
* Centralise Ã  la fin la gestion de l'absence de valeur

ğŸ’¡ Les [computation-expression-ce.md](computation-expression-ce.md "mention")ğŸ“ fournissent une syntaxe alternative + lÃ©gÃ¨re

## `Option` _vs_ `List`

Option â‰ƒ Liste de 0 ou 1 Ã©lÃ©ment â†’ cf. fonction `Option.toList`

```fsharp
let noneIsEmptyList       = Option.toList(None)   = []   // true
let someIsListWithOneItem = Option.toList(Some 1) = [1]  // true
```

â˜ Une `List` peut avoir + de 1 Ã©lÃ©ment \
â†’ Type `Option` modÃ©lise mieux l'absence de valeur que type `List`

ğŸ’¡ Module `Option` : beaucoup de mÃªme fonctions que module `List` \
â†’ `contains`, `count`, `exist`, `filter`, `fold`, `forall`, `map`

## `Option` _vs_ `null`

De part ses interactions avec la BCL, Fâ™¯ autorise parfois la valeur `null`

ğŸ‘‰ **Bonne pratique**\
â†’ Isoler ces cas de figure et wrapper dans un type `Option`\
â†’ Par exemple en utilisant la fonction `Option.ofObj`

```fsharp
let readLine (reader: System.IO.TextReader) =
    reader.ReadLine() |> Option.ofObj

    // Ã‰quivalent Ã  faire :
    match reader.ReadLine() with
    | null -> None
    | line -> Some line
```

## `Option` _vs_ `Nullable`

Type `System.Nullable<'T>` â‰ƒ `Option<'T>` en + limitÃ©

* â— Ne marche pas pour les types rÃ©fÃ©rences
* â— Manque comportement monadique i.e. fonctions `map` et `bind`
* â— En Fâ™¯, pas de magie comme en Câ™¯ / mot clÃ© `null`

ğŸ‘‰ `Option` est le type idiomatique en Fâ™¯

ğŸ’¡ On utilise le type `Nullable` en cas d'interop. Pour l'instancier : \
&#x20;  â†’ âŒ Contrairement au Câ™¯, le mot clÃ© `null` ne marche pas !\
&#x20;  â†’ ğŸ‘ L'emploie de `Unchecked.defaultof` marche mais n'est pas Ã©lÃ©gant.\
&#x20;  â†’ ğŸ‘ Utiliser l'un des constructeurs : `Nullable()` ou `Nullable(value)`

```fsharp
open System

let x: Nullable<int> = null
// âš ï¸ The type 'Nullable<int>' does not have 'null' as a proper value. To create a null value for a Nullable type use 'System.Nullable()' (FS43)

let x = Unchecked.defaultof<Nullable<int>>
// val x: Nullable<int> = <null>

let x' = Nullable<int>()
// val x': Nullable<int> = <null>

let y = Nullable(1)
// val y: Nullable<int> = 1
```
