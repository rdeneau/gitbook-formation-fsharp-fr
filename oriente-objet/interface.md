# Interface

## Syntaxe

Idem classe abstraite avec :

* Que des membres abstraits, d√©finis par leur signature
* Sans l'attribut `[<AbstractClass>]`&#x20;
* Avec l'attribut `[<Interface>]` de mani√®re optionnelle

```fsharp
type [accessibility-modifier] interface-name =
    abstract memberN : [ argument-typesN -> ] return-typeN
```

* Le **nom** d'une interface commence par `I` pour suivre la convention .NET
* Les **arguments** des m√©thodes peuvent √™tre nomm√©s :&#x20;

```fsharp
[<Interface>]
type IPrintable =
    abstract member Print : format:string -> unit
```

On peut aussi utiliser la syntaxe verbeuse avec un bloc `interface ... end`, \
‚Üí Non idiomatique sauf dans le cas d'une interface sans membre a.k.a _marker interface_.

```fsharp
type IMarker = interface end
```

## Impl√©mentation

2 mani√®res d'impl√©menter une interface :

1. Dans un type _(comme en C‚ôØ)_
2. Dans une expression objet üìç

### Dans un type

```fsharp
type IPrintable =
    abstract member Print : unit -> unit

type Range = { Min: int; Max: int } with
    interface IPrintable with
        member this.Print() = printfn $"[{this.Min}..{this.Max}]"
```

:warning: **Pi√®ge :** mot cl√© `interface` en F‚ôØ  ‚â† mot cl√© `interface` en C‚ôØ, Java, TS  ‚â° mot cl√© `implements` Java, TS

### Dans une expression objet

```fsharp
type IConsole =
    abstract ReadLine : unit -> string
    abstract WriteLine : string -> unit

let console =
    { new IConsole with
        member this.ReadLine () = Console.ReadLine ()
        member this.WriteLine line = printfn "%s" line }
```

## Impl√©mentation par d√©faut

F‚ôØ 5.0 supporte les interfaces d√©finissant des m√©thodes avec impl√©mentations par d√©faut √©crites en C‚ôØ 8+ mais ne permet pas de les d√©finir.

:warning: Mot cl√© `default` : support√© que dans les classes, pas dans les interfaces !

## Une interface F‚ôØ est explicite

Impl√©mentation d'une interface en F‚ôØ ‚â° Impl√©mentation explicite d'une interface en C‚ôØ

‚Üí Les m√©thodes de l'interface ne sont consommables que par _upcasting_ :

```fsharp
type IPrintable =
    abstract member Print : unit -> unit

type Range = { Min: int; Max: int } with
    interface IPrintable with
        member this.Print() = printfn $"[{this.Min}..{this.Max}]"

let range = { Min = 1; Max = 5 }
(range :> IPrintable).Print()  // Op√©rateur `:>` de upcast üìç
// [1..5]
```

## Impl√©menter une interface g√©n√©rique

```fsharp
type IValue<'T> =
    abstract member Get : unit -> 'T

type BiValue() =
    interface IValue<int> with
        member _.Get() = 1
    interface IValue<string> with
        member _.Get() = "hello"

let o = BiValue()
let i = (o :> IValue<int>).Get() // 1
let s = (o :> IValue<string>).Get() // "hello"
```

## H√©ritage

D√©fini avec mot cl√© `inherit`

```fsharp
type Base(x: int) =
    do
        printf "Base: "
        for i in 1..x do printf "%d " i
        printfn ""

type Child(y: int) =
    inherit Base(y * 2)
    do
        printf "Child: "
        for i in 1..y do printf "%d " i
        printfn ""

let child = Child(1)

// Base: 1 2 3 4
// Child: 1
```
