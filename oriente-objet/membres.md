# Membres

## DÃ©finition

Ã‰lÃ©ments complÃ©mentaires dans dÃ©finition d'un type _(classe, record, union)_

* _(Ã‰vÃ©nement)_
* MÃ©thode
* PropriÃ©tÃ©
* PropriÃ©tÃ© indexÃ©e
* Surcharge d'opÃ©rateur

## Membres statiques et d'instance

Membre statique : `static member member-name ...`

Membre d'instance :

* Membre concret : `member self-identifier.member-name ...`
* Membre abstrait : `abstract member member-name : type-signature`
* Membre virtuel = nÃ©cessite 2 dÃ©clarations
  1. Membre abstrait
  2. ImplÃ©mentation par dÃ©faut : `default self-identifier.member-name ...`
* Surcharge d'un membre virtuel : `override self-identifier.member-name ...`

â˜ `member-name` en PascalCase _(convention .NET)_

â˜ Pas de membre `protected` ou `private` !

## _Self identifier_

* En Câ™¯, Java, TypeScript : `this`
* En VB : `Me`
* En Fâ™¯ : au choix â†’ `this`, `self`, `me`, n'importe quel _identifier_ valide...

DÃ©finissable de 3 maniÃ¨res complÃ©mentaires :

1. Pour le constructeur primaire : avec `as` â†’ `type MyClass() as self = ...`
2. Pour un membre : `member me.Introduce() = printfn $"Hi, I'm {me.Name}"`
3. Pour un membre ne l'utilisant pas : avec `_` â†’ `member _.Hi() = printfn "Hi!"`\
   â˜ Depuis F# 6. Avant, on utilisait `__`&#x20;

## Appeler un membre

ğŸ’¡ Quasiment les mÃªmes rÃ¨gles qu'en Câ™¯

Appeler un membre d'instance Ã  l'intÃ©rieur du type \
â†’ PrÃ©fixer avec _self-identifier_ : `self-identifier.instance-member-name`

Appeler un membre d'instance depuis l'extÃ©rieur \
â†’ PrÃ©fixer avec le nom de l'instance : `instance-name.instance-member-name`

Appeler un membre statique \
â†’ PrÃ©fixer par le nom du type : `type-name.static-member-name`\
â˜ MÃªme Ã  l'intÃ©rieur du type c'est nÃ©cessaire en F# (alors que c'est optionnel en C#)

## MÃ©thode

MÃ©thode â‰ƒ Fonction attachÃ©e directement Ã  un type

2 formes de dÃ©claration des paramÃ¨tres :

1. ParamÃ¨tres curryfiÃ©s = Style FP
2. ParamÃ¨tres en tuple = Style OOP
   * Meilleure interop avec Câ™¯
   * Seul mode autorisÃ© pour les constructeurs
   * Support des paramÃ¨tres nommÃ©s, optionnels, en tableau
   * Support des surcharges _(overloads)_

```fsharp
// (1) Forme en tuple (la + classique)
type Product = { SKU: string; Price: float } with
    member this.TupleTotal(qty, discount) =
        (this.Price * float qty) - discount  // (A)

// (2) Forme currifiÃ©e
type Product' =
    { SKU: string; Price: float }
    member me.CurriedTotal qty discount =
        (me.Price * float qty) - discount  // (B)
```

â˜ `with` nÃ©cessaire en â‘  mais pas en â‘¡ Ã  cause de l'indentation     \
â†’ `end` peut terminer le bloc commencÃ© avec `with`

â˜ `this.Price` â’¶ et `me.Price` â’·     \
â†’ AccÃ¨s Ã  l'instance via le _self-identifier_ dÃ©fini par le membre

## Arguments nommÃ©s

Permet d'appeler une mÃ©thode tuplifiÃ©e en spÃ©cifiant le nom des paramÃ¨tres :

```fsharp
type SpeedingTicket() =
    member _.SpeedExcess(speed: int, limit: int) =
        speed - limit

    member x.CalculateFine() =
        if x.SpeedExcess(limit = 55, speed = 70) < 20 then 50.0 else 100.0
```

Pratique pour :

* Clarifier un usage pour le lecteur ou le compilateur (en cas de surcharges)
* Choisir l'ordre des arguments
* Ne spÃ©cifier que certains arguments, les autres Ã©tant optionnels

â˜ Les arguments _aprÃ¨s un argument nommÃ©_ sont forcÃ©ment nommÃ©s eux-aussi

## ParamÃ¨tres optionnels

Cette fonctionnalitÃ© permet d'appeler une mÃ©thode tuplifiÃ©e sans spÃ©cifier tous les paramÃ¨tres.

:warning: **Restriction :** Ce n'est que pour les mÃ©thodes tuplifiÃ©es, y compris les constructeurs. \
Mais cela ne marche ni pour les mÃ©thodes curryfiÃ©es, ni pour les fonctions, mÃªme tuplifiÃ©es.

ParamÃ¨tre optionnel :&#x20;

* DÃ©clarÃ© avec `?` devant son nom â†’ `?arg1: int`
* Dans le corps de la mÃ©thode, le paramÃ¨tre a en fait le type `Option` â†’ `arg1: int option`
  * On peut utiliser `defaultArg` pour indiquer la **valeur par dÃ©faut**
  * Mais cette valeur par dÃ©faut n'apparaÃ®t pas dans la signature, contrairement au C# !

Lors de l'appel de la mÃ©thode, on peut spÃ©cifier la valeur d'un paramÃ¨tre optionnel via un argument nommÃ©, ceci de 2 faÃ§ons possibles :&#x20;

* Nom et type d'origine â†’ `M(arg1 = 1)`
* Nom prÃ©fixÃ© par `?` et type wrappÃ© dans une `Option` â†’ `M(?arg1 = Some 1)`

**Exemple :**

```fsharp
type DuplexType = Full | Half

type Connection(?rate: int, ?duplex: DuplexType, ?parity: bool) =
    let duplex = defaultArg duplex Full
    let parity = defaultArg parity false
    let defaultRate = match duplex with Full -> 9600 | Half -> 4800
    let rate = defaultArg rate defaultRate
    do printfn "Baud Rate: %d - Duplex: %A - Parity: %b" rate duplex parity

let conn1 = Connection(duplex = Full)
let conn2 = Connection(?duplex = Some Half)
let conn3 = Connection(300, Half, true)
```

â˜ A noter : le _shadowing_ des paramÃ¨tres par des variables de mÃªme nom, par exemple `parity`

`let parity (* bool *) = defaultArg parity (* bool option *) Full`

### Interop .NET

Pour l'interop avec .NET, on utilise une syntaxe diffÃ©rente, basÃ©e sur des attributs, ce qui permet de pouvoir spÃ©cifier une valeur par dÃ©faut : \
`[<Optional; DefaultParameterValue(...)>] arg`

Les attributs `Optional` et `DefaultParameterValue` sont disponibles dans `open System.Runtime.InteropServices`.

Exemple : mÃ©thode pour tracer l'appel Ã  une fonction en rÃ©cupÃ©rant son nom grÃ¢ce Ã  l'attribut `CallerMemberName` dans `System.Runtime.CompilerServices` (\*)

```fsharp
open System.Runtime.CompilerServices
open System.Runtime.InteropServices

type Tracer() =
    static member trace(message: string,
                [<CallerMemberName; Optional; DefaultParameterValue("")>] memberName: string,
                [<CallerFilePath; Optional; DefaultParameterValue("")>] path: string,
                [<CallerLineNumber; Optional; DefaultParameterValue(0)>] line: int) =
        printfn $"Message: {message}"
        printfn $"Member name: {memberName}"
        printfn $"Source file path: {path}"
        printfn $"Source line number: {line}"

open type Tracer

let main() =
    trace "foo"

main();;
// Message: foo
// Member name: main
// Source file path: C:\Users\xxx\stdin
// Source line number: 18
```

(\*) Documentation :link: : [Caller information - F# | Microsoft Docs](https://docs.microsoft.com/en-us/dotnet/fsharp/language-reference/caller-information)

### Utilisation du pipe `|>`

On peut utiliser le _pipe_ avec une mÃ©thode Ã  :\
â€¢ 1 paramÃ¨tre,\
â€¢ 2 paramÃ¨tres dont le dernier est optionnel.

```fsharp
open System.Runtime.InteropServices

type LogLevel =
    | Trace = 1
    | Error = 2

type Logger() =
    member _.Log(message: string, [<Optional; DefaultParameterValue(LogLevel.Trace)>] level: LogLevel) =
        printfn $"[{level}] {message}"

let logger = Logger()

"Mon message" |> logger.Log
// > [Trace] Mon message
```

ğŸ’¡ Si l'on veut avoir un 3e paramÃ¨tre, il faut passer par une lambda intermÃ©diaire, un peu comme si on Ã©crivait nous-mÃªme la curryfication :

```fsharp
// ...
type Logger() =
    // ...
    member this.LogFrom(origin: string, [<Optional; DefaultParameterValue(LogLevel.Trace)>] level: LogLevel) =
        fun message -> this.Log($"Origin: {origin} | {message}", level)

let logger = Logger()

"Mon message" |> logger.LogFrom "Root"
// > [Trace] Origin: Root | Mon message
```

## Tableau de paramÃ¨tres

Permet de spÃ©cifier un nombre variable de paramÃ¨tres de mÃªme type â†’ Via attribut `System.ParamArray` sur le **dernier** argument de la mÃ©thode

```fsharp
open System

type MathHelper() =
    static member Max([<ParamArray>] items) =
        items |> Array.max

let x = MathHelper.Max(1, 2, 4, 5)  // 5
```

ğŸ’¡ Ã‰quivalent en Câ™¯ de `public static T Max<T>(params T[] items)`

## Appeler mÃ©thode Câ™¯ _TryXxx()_

â“ Comment appeler en Fâ™¯ une mÃ©thode Câ™¯ `bool TryXxx(args, out T outputArg)` ? _(Exemple : `int.TryParse`, `IDictionnary::TryGetValue`)_

* ğŸ‘ Utiliser Ã©quivalent Fâ™¯ de `out outputArg` mais utilise mutation ğŸ¤®
* âœ… Ne pas spÃ©cifier l'argument `outputArg`
  * Change le type de retour en tuple `bool * T`
  * `outputArg` devient le 2e Ã©lÃ©ment de ce tuple

```fsharp
  match System.Int32.TryParse text with
  | true, i  -> printf $"It's the number {value}."
  | false, _ -> printf $"{text} is not a number."
```

## Appeler mÃ©thode _Xxx(tuple)_

â“ Comment appeler une mÃ©thode dont 1er param est lui-mÃªme un tuple ?!

Essayons :

```fsharp
let friendsLocation = Map.ofList [ (0,0),"Peter" ; (1,0),"Jane" ]
// Map<(int * int), string>
let peter = friendsLocation.TryGetValue (0,0)
// ğŸ’¥ error FS0001: expression censÃ©e avoir le type `int * int`, pas `int`
```

ğŸ’¡ Explications : `TryGetValue(0,0)` = appel mÃ©thode en mode tuplifiÃ© \
â†’ SpÃ©cifie 2 paramÃ¨tres, `0` et `0`. \
â†’ `0` est un `int` alors qu'on attend un tuple `int * int` !

### Solutions

1. ğŸ˜• Doubles parenthÃ¨ses, mais syntaxe confusante
   * `friendsLocation.TryGetValue((0,0))`
2. ğŸ˜• _Backward pipe_, mais confusant aussi
   * `friendsLocation.TryGetValue <| (0,0)`
3. âœ… Utiliser une fonction plutÃ´t qu'une mÃ©thode
   * `friendsLocation |> Map.tryFind (0,0)`

## MÃ©thode _vs_ Fonction

| FonctionnalitÃ©                       | Fonction  | MÃ©thode currifiÃ©e | MÃ©thode tuplifiÃ©e |
| ------------------------------------ | --------- | ----------------- | ----------------- |
| Application partielle                | âœ… oui     | âœ… oui             | âŒ non             |
| Arguments nommÃ©s                     | âŒ non     | âŒ non             | âœ… oui             |
| ParamÃ¨tres optionnels                | âŒ non     | âŒ non             | âœ… oui             |
| Tableau de paramÃ¨tres                | âŒ non     | âŒ non             | âœ… oui             |
| Surcharge / _overload_               | âŒ non     | âŒ non             | âœ… oui  1ï¸âƒ£        |
| SensibilitÃ© Ã  l'ordre de dÃ©claration | âœ… oui     | âŒ non 2ï¸âƒ£         | âŒ non 2ï¸âƒ£         |
| SÃ©paration donnÃ©es / comportement    | âœ… oui 3ï¸âƒ£ | âŒ non             | âŒ non             |

<table><thead><tr><th>FonctionnalitÃ©</th><th width="170">Fonction</th><th>MÃ©thode statique</th><th>MÃ©thode d'instance</th></tr></thead><tbody><tr><td>Nommage</td><td>camelCase</td><td>PascalCase</td><td>PascalCase</td></tr><tr><td>Support du <code>inline</code></td><td>âœ… oui</td><td>âœ… oui</td><td>âœ… oui</td></tr><tr><td>RÃ©cursive</td><td>âœ… si <code>rec</code></td><td>âœ… oui</td><td>âœ… oui</td></tr><tr><td>InfÃ©rence de <code>x</code> dans</td><td><code>f x</code> â†’ âœ… oui</td><td>â–</td><td><code>x.M()</code> â†’ âŒ non</td></tr><tr><td>Passable en argument</td><td>âœ… oui : <code>g f</code></td><td>âœ… oui : <code>g T.M</code></td><td>âŒ non : <code>g x.M</code>  4ï¸âƒ£</td></tr></tbody></table>

**Notes**

1ï¸âƒ£ Si possible, prÃ©fÃ©rer des paramÃ¨tres optionnels Ã  des surcharges.

2ï¸âƒ£ Concernant l'ordre de dÃ©claration :&#x20;

* Il est malgrÃ© tout pris en compte dans le cas de membres gÃ©nÃ©riques\
  â†’ Cf. [https://stackoverflow.com/q/66358718/8634147](https://stackoverflow.com/q/66358718/8634147)
* Il est recommandÃ© de dÃ©clarer les membres de haut en bas, par homogÃ©nÃ©itÃ© avec le reste du code.

3ï¸âƒ£ La sÃ©paration des donnÃ©es et du comportement est un principe fort en programmation fonctionnelle. \
â†’ En orientÃ©-objet, on privilÃ©gie le polymorphisme ad-hoc : le comportement est en faÃ§ade et les donnÃ©es sont encapsulÃ©es dans les classes. \
â†’ Si on veut ajouter du comportement dans un pÃ©rimÃ¨tre + limitÃ©, on passe par une nouvelle classe, par composition si possible ou par hÃ©ritage ou par copie des donnÃ©es.\
â†’ En Câ™¯, on peut Ã©galement le faire au moyen de mÃ©thodes d'extension, au dÃ©triment du polymorphisme.\
â†’ En Fâ™¯, on privilÃ©gie la sÃ©paration des donnÃ©es et du comportement. Les donnÃ©es sont mises dans un type immutable pour contrer cette perte (relative) d'encapsulation. Cela permet de partager un type et de lui affecter des comportements spÃ©cifiques, localisÃ©s uniquement lÃ  oÃ¹ on en a besoin. \
â†’ Par exemple dans l'architecture [Elmish](https://elmish.github.io/elmish/), on fait en sorte de sÃ©parer _State_ et _View_ : chacun agit sur le _Model_ dans son registre, sans s'interfÃ©rer l'un l'autre : le _State_ gÃ¨re l'_Init_ du _Model_ et son _Update_ en fonction du Message reÃ§u, la _View_ s'occupe du rendu du _Model_. Si l'on souhaite ajouter des mÃ©thodes dans le _Model_, il faut faire attention Ã  ce qu'elles soient suffisamment gÃ©nÃ©riques pour Ãªtre utilisables Ã  la fois le _State_ et dans la _View_.

4ï¸âƒ£ Solutions alternatives :&#x20;

* Wrapper dans lambda : `g (fun x -> x.M())`
* Passer Ã  Fâ™¯8 : `g _.M()`

## MÃ©thodes statiques ou module compagnon ?

Le choix entre mÃ©thode et fonction se pose Ã©galement dans le cas de mÃ©thodes statiques ou de fonctions pour un module compagnon. L'usage dans le code appelant sera le mÃªme, Ã  la casse prÃ¨s (_camelCase_ vs _PascalCase_).

La question est pertinente en particulier dans le cas d'un _SmartConstructor_ ou d'une _Factory_ c'est-Ã -dire pour crÃ©er une instance du type. Ce qui peut faire pencher la balance du cÃ´tÃ© d'une mÃ©thode statique est la possibilitÃ© d'avoir des paramÃ¨tres optionnels que l'on peut mettre en relation avec des champs optionnels du type.

```fsharp
type PersonName =
    { FirstName  : string
      MiddleName : string option
      LastName   : string }
    
    static member Create(firstName, lastName, ?middleName) =
      { FirstName  = firstName
        MiddleName = middleName
        LastName   = lastName }

// Usage
let johnDoe = PersonName.Create("John", "Doe")

// Equivalent avec le constructeur primaire
let johnDoe' =
    { FirstName  = "John"
      LastName   = "Doe"
      MiddleName = None }


// ğŸ’¡ Egalement Ã©lÃ©gant pour dÃ©finir les 3 champs
let jfk = PersonName.Create("John", "Fitzgerald", "Kennedy")

// Equivalent avec le constructeur primaire
let jfk' =
    { FirstName  = "John"
      MiddleName = Some "Fitzgerald"
      LastName   = "Kennedy" }


// ğŸ’¡ Permet enfin de bÃ©nÃ©ficer des arguments nommÃ©s
let pierreLaurent = PersonName.Create(firstName = "Pierre", lastName = "Laurent")
```

ğŸ’¡Une telle mÃ©thode prÃ©sente un autre avantage : permettre de crÃ©er un `Record` en spÃ©cifiant le nom de son type.\
â†’ C'est parfois nÃ©cessaire, pour enlever une ambiguÃ¯tÃ© avec un autre `Record` de mÃªme structure.\
â†’ Cela n'est pas toujours pratique/Ã©lÃ©gant Ã  faire Ã  l'aide d'une annotation de type (`{ FirstName = ...} : PersonName`) ou en qualifiant les champs (`{ PersonName.FirstName = ... }`)

## PropriÃ©tÃ©s

â‰ƒ Sucre syntaxique masquant un _getter_ et/ou un _setter_

â†’ Permet d'utiliser la propriÃ©tÃ© comme s'il s'agissait d'un champ

2 faÃ§ons de dÃ©clarer une propriÃ©tÃ© :

### Getter

* Syntaxe 1 : `member this.Property = expression`
* Syntaxe 2 : `member this.Property with get () = expression`

:point\_up: **Remarques :**&#x20;

* `expression` peut accÃ©der aux variables et autres membres de l'objet grÃ¢ce au `this`
* `expression` est rÃ©-Ã©valuÃ©e Ã  chaque appel

Exemple 1 :&#x20;

```fsharp
type Person = { FirstName: string; LastName: string } with
    member it.FullName = $"{it.LastName.ToUpper()} {it.FirstName}"

let joe = { FirstName = "Joe"; LastName = "Dalton" }
let s = joe.FullName  // "DALTON Joe"
```

Exemple 2 : \
â†’ Permet de voir que la valeur d'un Getter est rÃ©Ã©valuÃ©e Ã  chaque appel \
â†’ Mais dans un tel cas prÃ©fÃ©rer une mÃ©thode Ã  une propriÃ©tÃ© â—

```fsharp
type Generator() =
    let random = new System.Random()
    member _.NextValue = random.Next()  // La valeur change Ã  chaque appel
```

### Automatique

* _Read-only_ :  `member val Property = value`\
  â†’ Equivalent d'une propriÃ©tÃ© C# `{ get; }`
* _Read/write_ : `member val Property = value with get, set`\
  â†’ Equivalent d'une propriÃ©tÃ© C# `{ get; set; }`

:point\_up: **Remarques :**&#x20;

* `value` est la valeur d'initialisation. Ce n'est pas le _backing field_ de la propriÃ©tÃ© car il est, lui, implicite, gÃ©nÃ©rÃ© par le compilateur.
* La propriÃ©tÃ© conserve la mÃªme valeur Ã  chaque appel au `get`. Seul l'Ã©ventuel `set` permet de modifier sa valeur.

Exemple 1 :&#x20;

```fsharp
type PersonName(first: string, last: string) =
    member val First = first
    member val Last = last
    member val Full = $"{last.ToUpper()} {first}"

let joe = PersonName(first = "Joe", last = "Dalton")
let s = joe.Full  // "DALTON Joe"
```

Exemple 2 :

```fsharp
type Generator() =
    let random = new System.Random()
    member val FirstValue = random.Next() // La valeur ne change pas Ã  chaque appel
```

### Autres cas

Dans les autres cas, la syntaxe est verbeuse _(_[_dÃ©tails_](https://docs.microsoft.com/en-us/dotnet/fsharp/language-reference/members/properties)_)_ \
ğŸ‘‰ PrÃ©fÃ©rer des mÃ©thodes, c'est plus explicite

### Pattern matching

:warning: Les propriÃ©tÃ©s ne sont pas dÃ©constructibles.

â†’ Peuvent participer Ã  un pattern matching que dans partie `when`

```fsharp
type Person = { First: string; Last: string } with
    member this.FullName = // Getter
        $"{this.Last.ToUpper()} {this.First}"

let joe = { First = "Joe"; Last = "Dalton" }
let { First = first } = joe  // val first : string = "Joe"
let { FullName = x } = joe
// ğŸ’¥ ~~~~~~~~ Error FS0039: undefined record label 'FullName'

let salut =
    match joe with
    | _ when joe.FullName = "DALTON Joe" -> "Salut, Joe !"
    | _ -> "Bonjour !"
// val salut : string = "Salut, Joe !"
```

## PropriÃ©tÃ©s indexÃ©es

Permet accÃ¨s par indice, comme si la classe Ã©tait un tableau : `instance.[index]` â†’ IntÃ©ressant pour une collection ordonnÃ©e, pour masquer l'implÃ©mentation

Mise en place en dÃ©clarant membre `Item`

```fsharp
member self-identifier.Item
    with get(index) =
        get-member-body
    and set index value =
        set-member-body
```

ğŸ’¡ PropriÃ©tÃ© _read-only_ (_write-only_) â†’ ne dÃ©clarer que le _getter_ (_setter_)

â˜ ParamÃ¨tre en tuple pour _getter_ â‰  paramÃ¨tres curryfiÃ©s _setter_

**Exemple :**

```fsharp
type Lang = En | Fr

type DigitLabel() =
    let labels = // Map<Lang, string[]>
        [| (En, [| "zero"; "one"; "two"; "three" |])
           (Fr, [| "zÃ©ro"; "un"; "deux"; "trois" |]) |] |> Map.ofArray

    member val Lang = En with get, set
    member me.Item with get(i) = labels.[me.Lang].[i]
    member _.En with get(i) = labels.[En].[i]

let digitLabel = DigitLabel()
let v1 = digitLabel.[1]     // "one"
digitLabel.Lang <- Fr
let v2 = digitLabel.[2]     // "deux"
let v3 = digitLabel.En(2)   // "two"
// ğŸ’¡ Notez la diffÃ©rence de syntaxe de l'appel Ã  la propriÃ©tÃ© `En`
```

## Slice

> Idem propriÃ©tÃ© indexÃ©e mais renvoie plusieurs valeurs

DÃ©finition : via mÃ©thode _(normale ou d'extension)_ `GetSlice(?start, ?end)`

Usage : via opÃ©rateur `..`

```fsharp
type Range = { Min: int; Max: int } with
    member this.GetSlice(min, max) =
        { Min = System.Math.Max(defaultArg min this.Min, this.Min)
        ; Max = System.Math.Min(defaultArg max this.Max, this.Max) }

let range = { Min = 1; Max = 5 }
let slice1 = range.[0..3] // { Min = 1; Max = 3 }
let slice2 = range.[2..]  // { Min = 2; Max = 5 }
```

## Surcharge d'opÃ©rateur

OpÃ©rateur surchargÃ© Ã  2 niveaux possibles :

1. Dans un module, sous forme de fonction
   * `let [inline] (operator-symbols) parameter-list = ...`
   * ğŸ‘‰ Cf. session sur les fonctions
   * â˜ LimitÃ© : 1 seule surcharge possible
2. Dans un type, sous forme de membre
   * `static member (operator-symbols) (parameter-list) =`
   * MÃªmes rÃ¨gles que pour la forme de fonction
   * ğŸ‘ Plusieurs surcharges possibles (N types Ã— P _overloads_)

**Exemple :**

```fsharp
type Vector(x: float, y: float) =
    member _.X = x
    member _.Y = y

    override me.ToString() =
        let format n = (sprintf "%+.1f" n)
        $"Vector (X: {format me.X}, Y: {format me.Y})"

    static member (*)(a, v: Vector) = Vector(a * v.X, a * v.Y)
    static member (*)(v: Vector, a) = a * v
    static member (~-)(v: Vector) = -1.0 * v
    static member (+) (v: Vector, w: Vector) = Vector(v.X + w.X, v.Y + w.Y)

let v1 = Vector(1.0, 2.0)   // Vector (X: +1.0, Y: +2.0)
let v2 = v1 * 2.0           // Vector (X: +2.0, Y: +4.0)
let v3 = 0.75 * v2          // Vector (X: +1.5, Y: +3.0)
let v4 = -v3                // Vector (X: -1.5, Y: -3.0)
let v5 = v1 + v4            // Vector (X: -0.5, Y: -1.0)
```
