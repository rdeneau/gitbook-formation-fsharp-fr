# Workflow asynchrone

## Besoins

1. Ne pas bloquer le thread courant en attendant un calcul long
2. Permettre calculs en parall√®le
3. Indiquer qu'un calcul peut prendre du temps

## Type `Async<'T>`

* Repr√©sente un calcul asynchrone
* Similaire au pattern `async/await` avant l'heure üìÜ
  * 2007 : `Async<'T>` F‚ôØ
  * 2012 : `Task<T>` .NET et pattern `async`/`await`
  * 2017 : `Promise` JavaScript et pattern `async`/`await`

## M√©thodes renvoyant un objet `Async`

`Async.AwaitTask(task: Task or Task<'T>) : Async<'T>`

* Conversion d'une `Task` (.NET) en `Async` (F‚ôØ)

`Async.Sleep(milliseconds or TimeSpan) : Async<unit>`

* ‚âÉ `await Task.Delay()` ‚â† `Thread.Sleep` ‚Üí ne bloque pas le thread courant

[FSharp.Control.CommonExtensions](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-control-commonextensions.html) : √©tend le type `System.IO.Stream`

* `AsyncRead(buffer: byte[], ?offset: int, ?count: int) : Async<int>`
* `AsyncWrite(buffer: byte[], ?offset: int, ?count: int) : Async<unit>`

[FSharp.Control.WebExtensions](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-control-webextensions.html) : √©tend le type `System.Net.WebClient`

* `AsyncDownloadData(address: Uri) : Async<byte[]>`
* `AsyncDownloadString(address: Uri) : Async<string`

## Lancement d'un calcul async

`Async.RunSynchronously(calc: Async<'T>, ?timeoutMs: int, ?cancellationToken) : 'T` ‚Üí Attend la fin du calcul mais bloque le thread appelant ! (‚â† `await` C‚ôØ) ‚ö†Ô∏è

`Async.Start(operation: Async<unit>, ?cancellationToken) : unit` \
‚Üí Ex√©cute l'op√©ration en background _(sans bloqu√© le thread appelant)_ \
‚ö†Ô∏è Si une exception survient, elle est "aval√©e" !

`Async.StartImmediate(calc: Async<'T>, ?cancellationToken) : unit` \
‚Üí Ex√©cute le calcul dans le thread appelant ! \
üí° Pratique dans une GUI pour la mettre √† jour : barre de progression...

`Async.StartWithContinuations(calc, continuations..., ?cancellationToken)` \
‚Üí Idem `Async.RunSynchronously` \
‚ö†Ô∏è ... avec 3 _callbacks_ de continuation : en cas de succ√®s ‚úÖ, d'exception üí• et d'annulation üõë

## Bloc `async { expression }`

_A.k.a. Async workflow_

Syntaxe pour √©crire de mani√®re s√©quentielle un calcul asynchrone ‚Üí Le r√©sultat du calcul est wrapp√© dans un objet `Async`

**Mots cl√©s**

* `return` ‚Üí valeur finale du calcul - `unit` si omis
* `let!` _(prononcer ¬´ let bang ¬ª)_    ‚Üí acc√®s au r√©sultat d'un sous-calcul async _(‚âÉ `await` en C‚ôØ)_
* `use!` ‚Üí idem `use` _(gestion d'un `IDisposable`)_ + `let!`
* `do!` ‚Üí idem `let!` pour calcul async sans retour (`Async<unit>`)

```fsharp
let repeat (computeAsync: int -> Async<string>) times = async {
    for i in [ 1..times ] do
        printf $"Start operation #{i}... "
        let! result = computeAsync i
        printfn $"Result: {result}"
}

let basicOp (num: int) = async {
    do! Async.Sleep 150
    return $"{num} * ({num} - 1) = {num * (num - 1)}"
}

repeat basicOp 5 |> Async.RunSynchronously

// Start operation #1... Result: 1 * (1 - 1) = 0
// Start operation #2... Result: 2 * (2 - 1) = 2
// Start operation #3... Result: 3 * (3 - 1) = 6
// Start operation #4... Result: 4 * (4 - 1) = 12
// Start operation #5... Result: 5 * (5 - 1) = 20
```

## Usage inappropri√© de `Async.RunSynchronously`

`Async.RunSynchronously` lance le calcul et renvoie son r√©sultat MAIS en bloquant le thread appelant ! Ne l'utiliser qu'en ¬´ bout de cha√Æne ¬ª et pas pour _unwrap_ des calculs asynchrones interm√©diaires ! Utiliser plut√¥t un bloc `async`.

```fsharp
// ‚ùå √Ä √©viter
let a = calcA |> Async.RunSynchronously
let b = calcB a |> Async.RunSynchronously
calcC b

// ‚úÖ √Ä pr√©f√©rer
async {
    let! a = calcA
    let! b = calcB a
    return calcC b
} |> Async.RunSynchronously
```

## Calculs en parall√®le

### `Async.Parallel`

`Async.Parallel(computations: seq<Async<'T>>, ?maxBranches) : Async<'T[]>`

‚âÉ `Task.WhenAll` : mod√®le [Fork-Join](https://en.wikipedia.org/wiki/Fork%E2%80%93join_model)

* _Fork_ : calculs lanc√©s en parall√®le
* Attente de la terminaison de tous les calculs
* _Join_ : agr√©gation des r√©sultats _(qui sont du m√™me type)_
  * dans le m√™me ordre que les calculs

```fsharp
let downloadSite (site: string) = async {
    do! Async.Sleep (100 * site.Length)
    printfn $"{site} ‚úÖ"
    return site.Length
}

[ "google"; "msn"; "yahoo" ]
|> List.map downloadSite  // string list
|> Async.Parallel         // Async<string[]>
|> Async.RunSynchronously // string[]
|> printfn "%A"

// msn ‚úÖ
// yahoo ‚úÖ
// google ‚úÖ
// [|6; 3; 5|]
```

### `Async.StartChild`

`Async.StartChild(calc: Async<'T>, ?timeoutMs: int) : Async<Async<'T>>`

Permet de lancer en parall√®le plusieurs calculs \
‚Üí ... dont les r√©sultats sont de types diff√©rents _(‚â† `Async.Parallel`)_

S'utilise dans bloc `async` avec 2 `let!` par calcul enfant _(cf. `Async<Async<'T>>`)_

Annulation conjointe üìç [#annulation-dune-tache](workflow-asynchrone.md#annulation-dune-tache "mention")\
‚Üí Le calcul enfant partage le jeton d‚Äôannulation du calcul parent

**Exemple :**

Soit le fonction `delay` \
‚Üí qui renvoie la valeur sp√©cifi√©e `x` \
‚Üí au bout de `ms` millisecondes

```fsharp
let delay (ms: int) x = async {
    do! Async.Sleep ms
    return x
}
```

üí° Minutage avec la directive FSI `#time` _(_[_doc_](https://docs.microsoft.com/en-us/dotnet/fsharp/tools/fsharp-interactive/#f-interactive-directive-reference)_)_

```fsharp
#time "on"  // --> Minutage activ√©
"a" |> delay 100 |> Async.RunSynchronously // R√©el : 00:00:00.111, Proc...
#time "off" // --> Minutage d√©sactiv√©
```

```fsharp
let inSeries = async {
    let! result1 = "a" |> delay 100
    let! result2 = 123 |> delay 200
    return (result1, result2)
}

let inParallel = async {
    let! child1 = "a" |> delay 100 |> Async.StartChild
    let! child2 = 123 |> delay 200 |> Async.StartChild
    let! result1 = child1
    let! result2 = child2
    return (result1, result2)
}
```

```fsharp
#time "on"
inSeries |> Async.RunSynchronously    // R√©el : 00:00:00.317, ...
#time "off"
#time "on"
inParallel |> Async.RunSynchronously  // R√©el : 00:00:00.205, ...
#time "off"
```

## Annulation d'une t√¢che

Se base sur un `CancellationToken/Source` par d√©faut ou explicite :

* `Async.RunSynchronously(computation, ?timeout, ?cancellationToken)`
* `Async.Start(computation, ?cancellationToken)`

D√©clencher l'annulation

* Token explicite + `cancellationTokenSource.Cancel()`
* Token explicite avec timeout `new CancellationTokenSource(timeout)`
* Token par d√©faut : `Async.CancelDefaultToken()` ‚Üí `OperationCanceledException`üí•

V√©rifier l'annulation

* Implicite : √† chaque mot cl√© dans bloc async : `let`, `let!`, `for`...
* Explicite local : `let! ct = Async.CancellationToken` puis `ct.IsCancellationRequested`
* Explicite global : `Async.OnCancel(callback)`

**Exemple :**

```fsharp
let sleepLoop = async {
    let stopwatch = System.Diagnostics.Stopwatch()
    stopwatch.Start()
    let log message = printfn $"""   [{stopwatch.Elapsed.ToString("s\.fff")}] {message}"""

    use! __ = Async.OnCancel (fun () ->
        log $"  Cancelled ‚ùå")

    for i in [ 1..5 ] do
        log $"Step #{i}..."
        do! Async.Sleep 500
        log $"  Completed ‚úÖ"
}

open System.Threading

printfn "1. RunSynchronously:"
Async.RunSynchronously(sleepLoop)

printfn "2. Start with CancellationTokenSource + Sleep + Cancel"
use manualCancellationSource = new CancellationTokenSource()
Async.Start(sleepLoop, manualCancellationSource.Token)
Thread.Sleep(1200)
manualCancellationSource.Cancel()

printfn "3. Start with CancellationTokenSource with timeout"
use cancellationByTimeoutSource = new CancellationTokenSource(1200)
Async.Start(sleepLoop, cancellationByTimeoutSource.Token)
```

R√©sultat :

```
1. RunSynchronously:
   [0.009] Step #1...
   [0.532]   Completed ‚úÖ
   [0.535] Step #2...
   [1.037]   Completed ‚úÖ
   [1.039] Step #3...
   [1.543]   Completed ‚úÖ
   [1.545] Step #4...
   [2.063]   Completed ‚úÖ
   [2.064] Step #5...
   [2.570]   Completed ‚úÖ
2. Start with CancellationTokenSource + Sleep + Cancel
   [0.000] Step #1...
   [0.505]   Completed ‚úÖ
   [0.505] Step #2...
   [1.011]   Completed ‚úÖ
   [1.013] Step #3...
   [1.234]   Cancelled ‚ùå
3. Start with CancellationTokenSource with timeout
... idem 2.
```
